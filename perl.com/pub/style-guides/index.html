<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" id="sixapart-standard">
<head>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
<meta name="generator" content="Movable Type Pro 5.02" />
<link rel="stylesheet" href="/pub/styles.css" type="text/css" />
<link rel="start" href="/pub/" title="Home" />
<link rel="alternate" type="application/atom+xml" title="Recent Entries" href="/pub/atom.xml" />
<script type="text/javascript" src="/pub/mt.js"></script>

<script type="text/javascript">

 var _gaq = _gaq || [];
 _gaq.push(['_setAccount', 'UA-50555-22']);
 _gaq.push(['_trackPageview']);

 (function() {
   var ga = document.createElement('script'); ga.type =
   'text/javascript'; ga.async = true;
   ga.src = ('https:' == document.location.protocol ? 'https://ssl' :
   'http://www') + '.google-analytics.com/ga.js';
   var s = document.getElementsByTagName('script')[0];
   s.parentNode.insertBefore(ga, s);
 })();

</script>
    <title>Perl.com: Style Guides Archives</title>


</head>
<body id="perl-com" class="mt-archive-listing mt-category-archive layout-wt">
    <div id="container">
        <div id="container-inner">


            <div id="header">
    <div id="header-inner">
        <div id="header-content">
        <div id="top_advert"> 
<!-- Put any landscape advert in here -->
<a href="http://www.perlfoundation.org/" target="_new">
<img src="/i/tpf_banner.png" width="468" height="60" /></a>
        </div> 



            <div id="header-name"><a href="/pub/" accesskey="1">Perl.com</a></div>
            <div id="header-description"></div>




        </div>
    </div>
</div>



            <div id="content">
                <div id="content-inner">


                    <div id="alpha">
                        <div id="alpha-inner">

                            
                            <h1 id="page-title" class="archive-title">Recently in <em>Style Guides</em> Category</h1>






                            
                            <div id="entry-798" class="entry-asset asset hentry">
    <div class="asset-header">
        <h2 class="asset-name entry-title"><a href="/pub/2005/07/bestpractices.html" rel="bookmark">Ten Essential Development Practices</a></h2>
        <div class="asset-meta">
            <span class="byline">
    
                By <span class="vcard author">Damian Conway</span> on <abbr class="published" title="2005-07-14T00:00:00-08:00">July 14, 2005 12:00 AM</abbr>
    
            </span>

            
            

        </div>
    </div>
    <div class="asset-content entry-content">

        <div class="asset-body">
            
<!-- sidebar begins -->
<!-- don't move sidebars-->
<!-- sidebar ends -->
<br clear="all" />
<p>The following ten tips come from <em><a
href="http://www.oreilly.com/catalog/perlbp/">Perl Best Practices</a></em>, a
new book of Perl coding and development guidelines by Damian Conway.</p>

<h3>1. Design the Module's Interface First</h3>

<p>The most important aspect of any module is not how it implements the
facilities it provides, but the way in which it provides those facilities in
the first place. If the module's API is too awkward, or too complex, or too
extensive, or too fragmented, or even just poorly named, developers will avoid
using it. They'll write their own code instead. In that way, a poorly designed
module can actually reduce the overall maintainability of a system.</p>

<p>Designing module interfaces requires both experience and creativity. Perhaps
the easiest way to work out how an interface should work is to "play test" it:
to write examples of code that will use the module before implementing the
module itself. These examples will not be wasted when the design is complete.
You can usually recycle them into demos, documentation examples, or the core of
a test suite.</p>

<a href="http://conferences.oreillynet.com/os2005/"><img src="http://conferences.oreillynet.com/images/os2005/banners/120x240.gif" width="120" height="240" hspace="10" vspace="10" border="0" alt="O'Reilly Open Source Convention 2005." align="right" /></a>

<p>The key, however, is to write that code as if the module were already
available, and write it the way you'd most like the module to work.</p>

<p>Once you have some idea of the interface you want to create, convert your
"play tests" into actual tests (see Tip #2). Then it's just a Simple Matter Of
Programming to make the module work the way that the code examples and the
tests want it to.</p>

<p>Of course, it may not be possible for the module to work the way you'd most
like, in which case attempting to implement it that way will help you determine
what aspects of your API are not practical, and allow you to work out what
might be an acceptable alternative.</p>

<h3>2. Write the Test Cases Before the Code</h3>

<p>Probably the single best practice in all of software development is writing
your test suite first.</p>

<p>A test suite is an executable, self-verifying specification of the behavior
of a piece of software. If you have a test suite, you can--at any point in the
development process--verify that the code works as expected. If you have a test
suite, you can--after any changes during the maintenance cycle--verify that the
code still works as expected.</p>

<p>Write the tests first. Write them as soon as you know what your interface
will be (see #1). Write them before you start coding your application or
module.  Unless you have tests, you have no unequivocal specification of what
the software should do, and no way of knowing whether it does it.</p>

<p>Writing tests always seems like a chore, and an unproductive chore at that:
you don't have anything to test yet, so why write tests? Yet most developers
will--almost automatically--write driver software to test their new module in
an ad hoc way:<a id="OLE_LINK428"></a></p>

<pre><code>&gt; cat try_inflections.pl

# Test my shiny new English inflections module...

use Lingua::EN::Inflect qw( inflect );

# Try some plurals (both standard and unusual inflections)...

my %plural_of = (
   'house'         =&gt; 'houses',
   'mouse'         =&gt; 'mice',
   'box'           =&gt; 'boxes',
   'ox'            =&gt; 'oxen',
   'goose'         =&gt; 'geese',
   'mongoose'      =&gt; 'mongooses', 
   'law'           =&gt; 'laws',
   'mother-in-law' =&gt; 'mothers-in-law',
);
 
# For each of them, print both the expected result and the actual inflection...

for my $word ( keys %plural_of ) {
   my $expected = $plural_of{$word};
   my $computed = inflect( "PL_N($word)" );
 
   print "For $word:\n", 
         "\tExpected: $expected\n",
         "\tComputed: $computed\n";
}</code></pre>

<p>A driver like that is actually harder to write than a test suite, because
you have to worry about formatting the output in a way that is easy to read.
It's also much harder to use the driver than it would be to use a test suite,
because every time you run it you have to wade though that formatted output
and verify "by eye" that everything is as it should be. That's also
error-prone; eyes are not optimized for picking out small differences in the
middle of large amounts of nearly identical text.</p>

<p>Instead of hacking together a driver program, it's easier to write a test
program using the standard <a
href="http://search.cpan.org/perldoc?Test::Simple">Test::Simple</a> module.
Instead of <code>print</code> statements showing what's being tested, you just
write calls to the <code>ok()</code> subroutine, specifying as its first
argument the condition under which things are okay, and as its second argument
a description of what you're actually testing:<a id="OLE_LINK429"></a></p>

<pre><code>&gt; cat inflections.t

use Lingua::EN::Inflect qw( inflect);

use Test::Simple qw( no_plan);

my %plural_of = (
   'mouse'         =&gt; 'mice',
   'house'         =&gt; 'houses',
   'ox'            =&gt; 'oxen',
   'box'           =&gt; 'boxes',
   'goose'         =&gt; 'geese',
   'mongoose'      =&gt; 'mongooses', 
   'law'           =&gt; 'laws',
   'mother-in-law' =&gt; 'mothers-in-law',
);

for my $word ( keys %plural_of ) {
   my $expected = $plural_of{$word};
   my $computed = inflect( "PL_N($word)" );

   ok( $computed eq $expected, "$word -&gt; $expected" );
}</code></pre> 













<p>Note that this code loads <code>Test::Simple</code> with the argument
<code>qw( no_plan )</code>. Normally that argument would be <code>tests =&gt;
count</code>, indicating how many tests to expect, but here the tests are
generated from the <code>%plural_of</code> table at run time, so the final
count will depend on how many entries are in that table. Specifying a fixed
number of tests when loading the module is useful if you happen know that
number at compile time, because then the module can also "meta-test:" verify
that you carried out all the tests you expected to.</p>

<p>The <code>Test::Simple</code> program is slightly more concise and readable
than the original driver code, and the output is much more compact and
informative:</p>

<pre><code>&gt; perl inflections.t

ok 1 - house -&gt; houses
ok 2 - law -&gt; laws
not ok 3 - mongoose -&gt; mongooses
#     Failed test (inflections.t at line 21)
ok 4 - goose -&gt; geese
ok 5 - ox -&gt; oxen
not ok 6 - mother-in-law -&gt; mothers-in-law
#     Failed test (inflections.t at line 21)
ok 7 - mouse -&gt; mice
ok 8 - box -&gt; boxes
1..8
# Looks like you failed 2 tests of 8. </code></pre>

<p>More importantly, this version requires far less effort to verify the
correctness of each test. You just scan down the left margin looking for a
<code>not</code> and a comment line.</p>

<p>You might prefer to use the <a
href="http://search.cpan.org/perldoc?Test::More">Test::More</a> module instead
of <code>Test::Simple</code>. Then you can specify the actual and expected
values separately, by using the <code>is()</code> subroutine, rather than
<code>ok()</code>:</p>

<pre><code>use Lingua::EN::Inflect qw( inflect );
use Test::More qw( no_plan ); # Now using more advanced testing tools

my %plural_of = (
   'mouse'         =&gt; 'mice',
   'house'         =&gt; 'houses',
   'ox'            =&gt; 'oxen',
   'box'           =&gt; 'boxes',
   'goose'         =&gt; 'geese',
   'mongoose'      =&gt; 'mongooses', 
   'law'           =&gt; 'laws',
   'mother-in-law' =&gt; 'mothers-in-law',
);

for my $word ( keys %plural_of ) {
   my $expected = $plural_of{$word};
   my $computed = inflect( "PL_N($word)" );

   # Test expected and computed inflections for string equality...
   is( $computed, $expected, "$word -&gt; $expected" );
}</code></pre> 

<p>Apart from no longer having to type the <code>eq</code> yourself, this
version also produces more detailed error messages:</p>

<pre><code>&gt; perl inflections.t

ok 1 - house -&gt; houses
ok 2 - law -&gt; laws
not ok 3 - mongoose -&gt; mongooses
#     Failed test (inflections.t at line 20)
#          got: 'mongeese'
#     expected: 'mongooses'
ok 4 - goose -&gt; geese
ok 5 - ox -&gt; oxen
not ok 6 - mother-in-law -&gt; mothers-in-law
#     Failed test (inflections.t at line 20)
#          got: 'mothers-in-laws'
#     expected: 'mothers-in-law'
ok 7 - mouse -&gt; mice
ok 8 - box -&gt; boxes
1..8
# Looks like you failed 2 tests of 8.</code></pre>

<p>The <a
href="http://search.cpan.org/perldoc?Test::Tutorial">Test::Tutorial</a>
documentation that comes with Perl 5.8 provides a gentle introduction to both
<code>Test::Simple</code> and <code>Test::More</code>.</p>

<h3>3. Create Standard POD Templates for Modules and Applications</h3>

<p>One of the main reasons documentation can often seem so unpleasant is the
"blank page effect." Many programmers simply don't know how to get started or
what to say.</p>

<p>Perhaps the easiest way to make writing documentation less forbidding (and
hence, more likely to actually occur) is to circumvent that initial empty screen
by providing a template that developers can cut and paste into their code.</p>

<p>For a module, that documentation template might look something like
this:</p>

<pre><code>=head1 NAME

&lt;Module::Name&gt; - &lt;One-line description of module's purpose&gt;

=head1 VERSION

The initial template usually just has:

This documentation refers to &lt;Module::Name&gt; version 0.0.1.

=head1 SYNOPSIS

   use &lt;Module::Name&gt;;

   # Brief but working code example(s) here showing the most common usage(s)
   # This section will be as far as many users bother reading, so make it as
   # educational and exemplary as possible.

=head1 DESCRIPTION

A full description of the module and its features.

May include numerous subsections (i.e., =head2, =head3, etc.).

=head1 SUBROUTINES/METHODS

A separate section listing the public components of the module's interface.

These normally consist of either subroutines that may be exported, or methods
that may be called on objects belonging to the classes that the module
provides.

Name the section accordingly.

In an object-oriented module, this section should begin with a sentence (of the
form "An object of this class represents ...") to give the reader a high-level
context to help them understand the methods that are subsequently described.

=head1 DIAGNOSTICS

A list of every error and warning message that the module can generate (even
the ones that will "never happen"), with a full explanation of each problem,
one or more likely causes, and any suggested remedies.

=head1 CONFIGURATION AND ENVIRONMENT

A full explanation of any configuration system(s) used by the module, including
the names and locations of any configuration files, and the meaning of any
environment variables or properties that can be set. These descriptions must
also include details of any configuration language used.

=head1 DEPENDENCIES

A list of all of the other modules that this module relies upon, including any
restrictions on versions, and an indication of whether these required modules
are part of the standard Perl distribution, part of the module's distribution,
or must be installed separately.

=head1 INCOMPATIBILITIES

A list of any modules that this module cannot be used in conjunction with.
This may be due to name conflicts in the interface, or competition for system
or program resources, or due to internal limitations of Perl (for example, many
modules that use source code filters are mutually incompatible).

=head1 BUGS AND LIMITATIONS

A list of known problems with the module, together with some indication of
whether they are likely to be fixed in an upcoming release.

Also, a list of restrictions on the features the module does provide: data types
that cannot be handled, performance issues and the circumstances in which they
may arise, practical limitations on the size of data sets, special cases that
are not (yet) handled, etc.

The initial template usually just has:

There are no known bugs in this module.

Please report problems to &lt;Maintainer name(s)&gt; (&lt;contact address&gt;)

Patches are welcome.

=head1 AUTHOR

&lt;Author name(s)&gt;  (&lt;contact address&gt;)

=head1 LICENSE AND COPYRIGHT

Copyright (c) &lt;year&gt; &lt;copyright holder&gt; (&lt;contact address&gt;).
All rights reserved.

followed by whatever license you wish to release it under.

For Perl code that is often just:

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L&lt;perlartistic&gt;.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.</code></pre>













<p>Of course, the specific details that your templates provide may vary from
those shown here, according to your other coding practices. The most likely
variation will be in the license and copyright, but you may also have specific
in-house conventions regarding version numbering, the grammar of diagnostic
messages, or the attribution of authorship.</p>

<h3>4. Use a Revision Control System</h3>

<p>Maintaining control over the creation and modification of your source code
is utterly essential for robust team-based development.  And not just over
source code: you should be revision controlling your documentation, and data
files, and document templates, and makefiles, and style sheets, and change
logs, and any other resources your system requires.</p>

<p>Just as you wouldn't use an editor without an Undo command or a word
processor that can't merge documents, so too you shouldn't use a file system
you can't rewind, or a development environment that can't integrate the work
of many contributors.</p>

<p>Programmers make mistakes, and occasionally those mistakes will be
catastrophic. They will reformat the disk containing the most recent version of the
code. Or they'll mistype an editor macro and write zeros all through the source
of a critical core module. Or two developers will unwittingly edit the same
file at the same time and half their changes will be lost. Revision control
systems can prevent those kinds of problems.</p>

<p>Moreover, occasionally the very best debugging technique is to just give up,
stop trying to get yesterday's modifications to work correctly, roll the code
back to a known stable state, and start over again. Less drastically, comparing
the current condition of your code with the most recent stable version from
your repository (even just a line-by-line <code>diff</code>) can often help you isolate your
recent "improvements" and work out which of them is the problem.</p>

<p>Revision control systems such as RCS, CVS, Subversion, Monotone, <code>darcs</code>,
Perforce, GNU arch, or BitKeeper can protect against calamities, and ensure
that you always have a working fallback position if maintenance goes horribly
wrong. The various systems have different strengths and limitations, many of
which stem from fundamentally different views on what exactly revision control
is. It's a good idea to audition the various revision control systems, and find
the one that works best for you.  <em>Pragmatic Version Control Using
Subversion</em>, by Mike Mason (Pragmatic Bookshelf, 2005) and <a href="http://www.oreilly.com/catalog/cvs/"><em>Essential
CVS</em></a>, by Jennifer Vesperman (O'Reilly, 2003) are useful starting
points.</p>

<h3>5. Create Consistent Command-Line Interfaces</h3>

<p>Command-line interfaces have a strong tendency to grow over time, accreting
new options as you add features to the application. Unfortunately, the
evolution of such interfaces is rarely designed, managed, or controlled, so the
set of flags, options, and arguments that a given application accepts are
likely to be ad hoc and unique.</p>

<p>This also means they're likely to be inconsistent with the unique ad hoc
sets of flags, options, and arguments that other related applications provide.
The result is inevitably a suite of programs, each of which is driven in a
distinct and idiosyncratic way. For example:</p>

<pre><code>&gt; orchestrate source.txt -to interim.orc

&gt; remonstrate +interim.rem -interim.orc 

&gt; fenestrate  --src=interim.rem --dest=final.wdw
Invalid input format

&gt; fenestrate --help
Unknown option: --help.
Type 'fenestrate -hmo' for help</code></pre>

<p>Here, the <code>orchestrate</code> utility expects its input file as its
first argument, while the <code>-to</code> flag specifies its output file. The
related <code>remonstrate</code> tool uses <code>-infile</code> and
<code>+outfile</code> options instead, with the output file coming first. The
<code>fenestrate</code> program seems to require GNU-style "long options:"
<code>--src=infile</code> and <code>--dest=outfile</code>, except, apparently,
for its oddly named help flag. All in all, it's a mess.</p>

<p>When you're providing a suite of programs, all of them should appear to
work the same way, using the same flags and options for the same features
across all applications. This enables your users to take advantage of existing
knowledge--instead of continually asking you.</p>

<p>Those three programs should work like this:</p>

<pre><code>&gt; orchestrate -i source.txt -o dest.orc

&gt; remonstrate -i source.orc -o dest.rem

&gt; fenestrate  -i source.rem -o dest.wdw
Input file ('source.rem') not a valid Remora file
(type "fenestrate --help" for help)

&gt; fenestrate --help
fenestrate - convert Remora .rem files to Windows .wdw format
Usage: fenestrate [-i &lt;infile&gt;] [-o &lt;outfile&gt;] [-cstq] [-h|-v]
Options:
   -i &lt;infile&gt; Specify input source [default: STDIN]
   -o &lt;outfile&gt; Specify output destination [default: STDOUT]
   -c Attempt to produce a more compact representation
   -h Use horizontal (landscape) layout
   -v Use vertical (portrait) layout
   -s Be strict regarding input
   -t Be extra tolerant regarding input
   -q Run silent
   --version Print version information
   --usage Print the usage line of this summary
   --help Print this summary
   --man Print the complete manpage</code></pre>

<p>Here, every application that takes input and output files uses the same two
flags to do so. A user who wants to use the <code>substrate</code> utility (to convert that
final .wdw file to a subroutine) is likely to be able to guess
correctly the required syntax:</p>

<pre><code>&gt; substrate  -i dest.wdw -o dest.sub</code></pre>

<p>Anyone who can't guess that probably can guess that:</p>

<pre><code>&gt; substrate --help</code></pre>

<p>is likely to render aid and comfort.</p>













<p>A large part of making interfaces consistent is being consistent in
specifying the individual components of those interfaces. Some conventions that
may help to design consistent and predictable interfaces include:</p>

<ul>

<li><p>Require a flag preceding every piece of command-line data, except
filenames.</p>

<p>Users don't want to have to remember that your application requires "input
file, output file, block size, operation, fallback strategy," and requires
them in that precise order:</p>

<pre><code>&gt; lustrate sample_data proc_data 1000 normalize log</code></pre>

<p>They want to be able to say explicitly what they mean, in any order that
suits them:</p>

<pre><code>&gt; lustrate sample_data proc_data -op=normalize -b1000 --fallback=log</code></pre></li>

<li><p>Provide a flag for each filename, too, especially when a program can be
given files for different purposes.</p>

<p>Users might also not want to remember the order of the two positional
filenames, so let them label those arguments as well, and specify them in
whatever order they prefer:</p>

<pre><code>&gt; lustrate -i sample_data -op normalize -b1000 --fallback log -o proc_data</code></pre></li>

<li><p>Use a single <code>-</code> prefix for short-form flags, up to three
letters (<code>-v</code>, <code>-i</code>, <code>-rw</code>, <code>-in</code>,
<code>-out</code>).</p>

<p>Experienced users appreciate short-form flags as a way of reducing typing
and limiting command-line clutter. Don't make them type two dashes in these
shortcuts.</p></li>

<li><p>Use a double <code>--</code> prefix for longer flags
(<code>--verbose</code>, <code>--interactive</code>, <code>--readwrite</code>,
<code>--input</code>, <code>--output</code>).</p>

<p>Flags that are complete words improve the readability of a command line (in
a shell script, for example). The double dash also helps to distinguish between
the longer flag name and any nearby file names.</p></li>

<li><p>If a flag expects an associated value, allow an optional <code>=</code> between the
flag and the value.</p>

<p>Some people prefer to visually associate a value with its preceding
flag:</p>

<pre><code>&gt; lustrate -i=sample_data -op=normalize -b=1000 --fallback=log -o=proc_data</code></pre>

<p>Others don't:</p>

<pre><code>&gt; lustrate -i sample_data -op normalize -b1000 --fallback log -o proc_data</code></pre>

<p>Still others want a bit each way:</p>

<pre><code>&gt; lustrate -i sample_data -o proc_data -op=normalize -b=1000 --fallback=log</code></pre>

<p>Let the user choose.</p></li>

<li><p>Allow single-letter options to be "bundled" after a single dash.</p>

<p>It's irritating to have to type repeated dashes for a series of flags:</p>

<pre><code>&gt; lustrate -i sample_data -v -l -x</code></pre>

<p>Allow experienced users to also write:</p>

<pre><code>&gt; lustrate -i sample_data -vlx</code></pre></li>

<li><p>Provide a multi-letter version of every single-letter flag.</p>

<p>Short-form flags may be nice for experienced users, but they can be
troublesome for new users: hard to remember and even harder to recognize. Don't
force people to do either. Give them a verbose alternative to every concise
flag; full words that are easier to remember, and also more self-documenting in
shell scripts.</p></li>

<li><p>Always allow <code>-</code> as a special filename.</p>

<p>A widely used convention is that a dash (<code>-</code>) where an input file
is expected means "read from standard input," and a dash where an output file
is expected means "write to standard output."</p></li>

<li><p>Always allow <code>--</code> as a file list marker.</p>

<p>Another widely used convention is that the appearance of a double dash
(<code>--</code>) on the command line marks the end of any flagged options, and
indicates that the remaining arguments are a list of filenames, even if some of
them look like flags.</p></li>

</ul>

<h3>6. Agree Upon a Coherent Layout Style and Automate It with <code>perltidy</code></h3>

<p>Formatting. Indentation. Style. Code layout. Whatever you choose to call it,
it's one of the most contentious aspects of programming discipline. More and
bloodier wars have been fought over code layout than over just about any other
aspect of coding.</p>

<p>What is the best practice here? Should you use classic Kernighan and
Ritchie style? Or go with BSD code formatting? Or adopt the layout scheme
specified by the GNU project? Or conform to the Slashcode coding
guidelines?</p>

<p>Of course not! Everyone knows that <em>&lt;insert your personal coding style here&gt;</em>
is the One True Layout Style, the only sane choice, as ordained by <em>&lt;insert your
favorite Programming Deity here&gt;</em> since Time Immemorial! Any other choice is
manifestly absurd, willfully heretical, and self-evidently a Work of
Darkness!</p>

<p>That's precisely the problem. When deciding on a layout style, it's hard to
decide where rational choices end and rationalized habits begin.</p>

<p>Adopting a coherently designed approach to code layout, and then applying
that approach consistently across all your coding, is fundamental to best-practice programming. Good layout can improve the readability of a program,
help detect errors within it, and make the structure of your code much easier
to comprehend. Layout matters.</p>

<p>However, most coding styles--including the four mentioned earlier--confer
those benefits almost equally well. While it's true that having a consistent
code layout scheme matters very much indeed, the particular code layout scheme
you ultimately decide upon does not matter at all!  All that matters is that
you adopt a single, coherent style; one that works for your entire programming
team, and, having agreed upon that style, that you then apply it consistently
across all your development.</p>













<p>In the long term, it's best to train yourself and your team to code in a
consistent, rational, and readable style. However, the time and commitment
necessary to accomplish that isn't always available. In such cases, a
reasonable compromise is to prescribe a standard code-formatting tool that must
be applied to all code before it's committed, reviewed, or otherwise displayed
in public.</p>

<p>There is now an excellent code formatter available for Perl: <a
href="http://perltidy.sourceforge.net/"><code>perltidy</code></a>. It provides an extensive
range of user-configurable options for indenting, block delimiter positioning,
column-like alignment, and comment positioning.</p>

<p>Using <code>perltidy</code>, you can convert code like this:</p>

<pre><code>if($sigil eq '$'){
   if($subsigil eq '?'){ 
       $sym_table{substr($var_name,2)}=delete $sym_table{locate_orig_var($var)};
       $internal_count++;$has_internal{$var_name}++
   } else {
       ${$var_ref} =
           q{$sym_table{$var_name}}; $external_count++; $has_external{$var_name}++;
}} elsif ($sigil eq '@'&amp;&amp;$subsigil eq '?') {
   @{$sym_table{$var_name}} = grep
       {defined $_} @{$sym_table{$var_name}};
} elsif ($sigil eq '%' &amp;&amp; $subsigil eq '?') {
delete $sym_table{$var_name}{$EMPTY_STR}; } else
{
${$var_ref}
=
q{$sym_table{$var_name}}
}</code></pre>

<p>into something readable:</p>

<pre><code>if ( $sigil eq '$' ) {
   if ( $subsigil eq '?' ) {
       $sym_table{ substr( $var_name, 2 ) }
           = delete $sym_table{ locate_orig_var($var) };
       $internal_count++;
       $has_internal{$var_name}++;
   }
   else {
       ${$var_ref} = q{$sym_table{$var_name}};
       $external_count++;
       $has_external{$var_name}++;
   }
}
elsif ( $sigil eq '@' &amp;&amp; $subsigil eq '?' ) {
   @{ $sym_table{$var_name} }
       = grep {defined $_} @{ $sym_table{$var_name} };
}
elsif ( $sigil eq '%' &amp;&amp; $subsigil eq '?' ) {
   delete $sym_table{$var_name}{$EMPTY_STR};
}
else {
   ${$var_ref} = q{$sym_table{$var_name}};
}</code></pre>

<p>Mandating that everyone use a common tool to format their code can also be a
simple way of sidestepping the endless objections, acrimony, and dogma that
always surround any discussion on code layout. If <code>perltidy</code> does all the work
for them, then it will cost developers almost no effort to adopt the new
guidelines. They can simply set up an editor macro that will "straighten" their
code whenever they need to.</p>

<h3>7. Code in Commented Paragraphs</h3>

<p>A paragraph is a collection of statements that accomplish a single task: in
literature, it's a series of sentences conveying a single idea; in programming,
a series of instructions implementing a single step of an algorithm.</p>

<p>Break each piece of code into sequences that achieve a single task, placing
a single empty line between each sequence. To further improve the
maintainability of the code, place a one-line comment at the start of each such
paragraph, describing what the sequence of statements does. Like so:</p>

<pre><code># Process an array that has been recognized...
sub addarray_internal {
   my ($var_name, $needs_quotemeta) = @_;

   # Cache the original...
   $raw .= $var_name;

   # Build meta-quoting code, if requested...
   my $quotemeta = $needs_quotemeta ?  q{map {quotemeta $_} } : $EMPTY_STR;

   # Expand elements of variable, conjoin with ORs...
   my $perl5pat = qq{(??{join q{|}, $quotemeta \@{$var_name}})};

   # Insert debugging code if requested...
   my $type = $quotemeta ? 'literal' : 'pattern';
   debug_now("Adding $var_name (as $type)");
   add_debug_mesg("Trying $var_name (as $type)");

   return $perl5pat;
}</code></pre>

<p>Paragraphs are useful because humans can focus on only a few pieces of
information at once. Paragraphs are one way of aggregating small amounts of
related information, so that the resulting "chunk" can fit into a single slot
of the reader's limited short-term memory. Paragraphs enable the physical
structure of a piece of writing to reflect and emphasize its logical
structure.</p>

<p>Adding comments at the start of each paragraph further enhances the chunking
by explicitly summarizing the purpose of each chunk (note: the purpose, not the
behavior). Paragraph comments need to explain why the code is there and what it
achieves, not merely paraphrase the precise computational steps it's
performing.</p>













<p>Note, however, that the contents of paragraphs are only of secondary
importance here. It is the vertical gaps separating each paragraph that are
critical. Without them, the readability of the code declines dramatically, even
if the comments are retained:</p>

<pre><code>sub addarray_internal {
   my ($var_name, $needs_quotemeta) = @_;
   # Cache the original...
   $raw .= $var_name;
   # Build meta-quoting code, if required...
   my $quotemeta = $needs_quotemeta ?  q{map {quotemeta $_} } : $EMPTY_STR;
   # Expand elements of variable, conjoin with ORs...
   my $perl5pat = qq{(??{join q{|}, $quotemeta \@{$var_name}})};
   # Insert debugging code if requested...
   my $type = $quotemeta ? 'literal' : 'pattern';
   debug_now("Adding $var_name (as $type)");
   add_debug_mesg("Trying $var_name (as $type)");
   return $perl5pat;
}</code></pre>

<h3>8. Throw Exceptions Instead of Returning Special Values or Setting
Flags</h3>

<p>Returning a special error value on failure, or setting a special error flag,
is a very common error-handling technique.  Collectively, they're the basis for
virtually all error notification from Perl's own built-in functions. For
example, the built-ins <code>eval</code>, <code>exec</code>, <code>flock</code>,
<code>open</code>, <code>print</code>, <code>stat</code>, and
<code>system</code> all return special values on error. Unfortunately, they
don't all use the same special value. Some of them also set a flag on failure.
Sadly, it's not always the same flag. See the <a
href="http://perldoc.perl.org/perlfunc.html"><code>perlfunc</code></a> manpage for the gory
details.</p>

<p>Apart from the obvious consistency problems, error notification via flags
and return values has another serious flaw: developers can silently ignore
flags and return values, and ignoring them requires absolutely no effort on the
part of the programmer. In fact, in a void context, ignoring return values is
Perl's default behavior. Ignoring an error flag that has suddenly appeared in
a special variable is just as easy: you simply don't bother to check the
variable.</p>

<p>Moreover, because ignoring a return value is the void-context default,
there's no syntactic marker for it. There's no way to look at a program and
immediately see where a return value is deliberately being ignored, which means
there's also no way to be sure that it's not being ignored accidentally.</p>

<p>The bottom line: regardless of the programmer's (lack of) intention, an
error indicator is being ignored. That's not good programming.</p>

<p>Ignoring error indicators frequently causes programs to propagate errors in
entirely the wrong direction. For example:</p>

<pre><code># Find and open a file by name, returning the filehandle
# or undef on failure...
sub locate_and_open {
   my ($filename) = @_;

   # Check acceptable directories in order...
   for my $dir (@DATA_DIRS) {
       my $path = "$dir/$filename";

       # If file exists in an acceptable directory, open and return it...
       if (-r $path) {
           open my $fh, '&lt;', $path;
           return $fh;
       }
   }

   # Fail if all possible locations tried without success...
   return;
}

# Load file contents up to the first &lt;DATA/&gt; marker...
sub load_header_from {
   my ($fh) = @_;

   # Use DATA tag as end-of-"line"...
   local $/ = '&lt;DATA/&gt;';

   # Read to end-of-"line"...
   return &lt;$fh&gt;;
}

# and later...
for my $filename (@source_files) {
   my $fh = locate_and_open($filename);
   my $head = load_header_from($fh);
   print $head;
}</code></pre>

<p>The <code>locate_and_open()</code> subroutine simply assumes that the call
to <code>open</code> works, immediately returning the filehandle
(<code>$fh</code>), whatever the actual outcome of the <code>open</code>. Presumably, the
expectation is that whoever calls <code>locate_and_open()</code> will check
whether the return value is a valid filehandle.</p>

<p>Except, of course, "whoever" doesn't check. Instead of testing for failure,
the main <code>for</code> loop takes the failure value and immediately
propagates it "across" the block, to the rest of the statements in the loop.
That causes the call to <code>loader_header_from()</code> to propagate the
error value "downwards." It's in that subroutine that the attempt to treat the
failure value as a filehandle eventually kills the program:</p>

<pre><code>readline() on unopened filehandle at demo.pl line 28.</code></pre>

<p>Code like that--where an error is reported in an entirely different part of
the program from where it actually occurred--is particularly onerous to
debug.</p>

<p>Of course, you could argue that the fault lies squarely with whoever wrote
the loop, for using <code>locate_and_open()</code> without checking its return
value.  In the narrowest sense, that's entirely correct--but the deeper fault
lies with whoever actually wrote <code>locate_and_open()</code> in the first
place, or at least, whoever assumed that the caller would always check its
return value.</p>













<p>Humans simply aren't like that. Rocks almost never fall out of the sky, so
humans soon conclude that they never do, and stop looking up for them.  Fires
rarely break out in their homes, so humans soon forget that they might, and
stop testing their smoke detectors every month. In the same way, programmers
inevitably abbreviate "almost never fails" to "never fails," and then simply
stop checking.</p>

<p>That's why so very few people bother to verify their <code>print</code>
statements:</p>

<pre><code>if (!print 'Enter your name: ') {
   print {*STDLOG} warning =&gt; 'Terminal went missing!'
}</code></pre>

<p>It's human nature to "trust but not verify."</p>

<p>Human nature is why returning an error indicator is not best practice.
Errors are (supposed to be) unusual occurrences, so error markers will almost
never be returned. Those tedious and ungainly checks for them will almost never
do anything useful, so eventually they'll be quietly omitted. After all,
leaving the tests off almost always works just fine. It's so much easier not to
bother. Especially when not bothering is the default!</p>

<p>Don't return special error values when something goes wrong; throw an
exception instead. The great advantage of exceptions is that they reverse the
usual default behaviors, bringing untrapped errors to immediate and urgent
attention. On the other hand, ignoring an exception requires a deliberate and
conspicuous effort: you have to provide an explicit <code>eval</code> block to
neutralize it.</p>

<p>The <code>locate_and_open()</code> subroutine would be much cleaner and more
robust if the errors within it threw exceptions:</p>

<pre><code># Find and open a file by name, returning the filehandle
# or throwing an exception on failure...
sub locate_and_open {
   my ($filename) = @_;

   # Check acceptable directories in order...
   for my $dir (@DATA_DIRS) {
       my $path = "$dir/$filename";

       # If file exists in acceptable directory, open and return it...
       if (-r $path) {
           open my $fh, '&lt;', $path
               or croak( "Located $filename at $path, but could not open");
           return $fh;
       }
   }

   # Fail if all possible locations tried without success...
   croak( "Could not locate $filename" );
}

# and later...
for my $filename (@source_files) {
   my $fh = locate_and_open($filename);
   my $head = load_header_from($fh);
   print $head;
}</code></pre>

<p>Notice that the main <code>for</code> loop didn't change at all. The
developer using <code>locate_and_open()</code> still assumes that nothing can
go wrong. Now there's some justification for that expectation, because if
anything does go wrong, the thrown exception will automatically terminate the
loop.</p>

<p>Exceptions are a better choice even if you are the careful type who
religiously checks every return value for failure:</p>

<pre><code>SOURCE_FILE:
for my $filename (@source_files) {
   my $fh = locate_and_open($filename);
   next SOURCE_FILE if !defined $fh;
   my $head = load_header_from($fh);
   next SOURCE_FILE if !defined $head;
   print $head;
}</code></pre>

<p>Constantly checking return values for failure clutters your code with
validation statements, often greatly decreasing its readability. In contrast,
exceptions allow an algorithm to be implemented without having to intersperse
any error-handling infrastructure at all. You can factor the error-handling out
of the code and either relegate it to after the surrounding <code>eval</code>,
or else dispense with it entirely:</p>

<pre><code>for my $filename (@directory_path) {

   # Just ignore any source files that don't load...
   eval {
       my $fh = locate_and_open($filename);
       my $head = load_header_from($fh);
       print $head;
   }
}</code></pre>

<h3>9. Add New Test Cases Before you Start Debugging</h3>

<p>The first step in any debugging process is to isolate the incorrect
behavior of the system, by producing the shortest demonstration of it that you
reasonably can. If you're lucky, this may even have been done for you:</p>

<pre><code>To: DCONWAY@cpan.org
From: sascha@perlmonks.org
Subject: Bug in inflect module

Zdravstvuite,

I have been using your Lingua::EN::Inflect module to normalize terms in a
data-mining application I am developing, but there seems to be a bug in it,
as the following example demonstrates:

   use Lingua::EN::Inflect qw( PL_N );
   print PL_N('man'), "\n";       # Prints "men", as expected
   print PL_N('woman'), "\n";     # Incorrectly prints "womans"</code></pre>

<p>Once you have distilled a short working example of the bug, convert it to a
series of tests, such as:</p>

<pre><code>use Lingua::EN::Inflect qw( PL_N );
use Test::More qw( no_plan );
is(PL_N('man') ,  'men', 'man -&gt; men'     );
is(PL_N('woman'), 'women', 'woman -&gt; women' );</code></pre>













<p>Don't try to fix the problem straight away, though. Instead, immediately add
those tests to your test suite. If that testing has been well set up, that can
often be as simple as adding a couple of entries to a table:</p>

<pre><code>my %plural_of = (
   'mouse'         =&gt; 'mice',
   'house'         =&gt; 'houses',
   'ox'            =&gt; 'oxen',
   'box'           =&gt; 'boxes',
   'goose'         =&gt; 'geese',
   'mongoose'      =&gt; 'mongooses', 
   'law'           =&gt; 'laws',
   'mother-in-law' =&gt; 'mothers-in-law', 

   # Sascha's bug, reported 27 August 2004...
   'man'           =&gt; 'men',
   'woman'         =&gt; 'women',
);</code></pre>

<p>The point is: if the original test suite didn't report this bug, then that
test suite was broken. It simply didn't do its job (finding bugs) adequately.
Fix the test suite first by adding tests that cause it to fail:</p>

<pre><code>&gt; perl inflections.t
ok 1 - house -&gt; houses
ok 2 - law -&gt; laws
ok 3 - man -&gt; men
ok 4 - mongoose -&gt; mongooses
ok 5 - goose -&gt; geese
ok 6 - ox -&gt; oxen
not ok 7 - woman -&gt; women
#     Failed test (inflections.t at line 20)
#          got: 'womans'
#     expected: 'women'
ok 8 - mother-in-law -&gt; mothers-in-law
ok 9 - mouse -&gt; mice
ok 10 - box -&gt; boxes
1..10
# Looks like you failed 1 tests of 10.</code></pre>

<p>Once the test suite is detecting the problem correctly, then you'll be able
to tell when you've correctly fixed the actual bug, because the tests will once
again fall silent.</p>

<p>This approach to debugging is most effective when the test suite covers the
full range of manifestations of the problem. When adding test cases for a bug,
don't just add a single test for the simplest case. Make sure you include the
obvious variations as well:</p>

<pre><code>my %plural_of = (
   'mouse'         =&gt; 'mice',
   'house'         =&gt; 'houses',
   'ox'            =&gt; 'oxen',
   'box'           =&gt; 'boxes',
   'goose'         =&gt; 'geese',
   'mongoose'      =&gt; 'mongooses', 
   'law'           =&gt; 'laws',
   'mother-in-law' =&gt; 'mothers-in-law', 

   # Sascha's bug, reported 27 August 2004...
   'man'           =&gt; 'men',
   'woman'         =&gt; 'women',
   'human'         =&gt; 'humans',
   'man-at-arms'   =&gt; 'men-at-arms', 
   'lan'           =&gt; 'lans',
   'mane'          =&gt; 'manes',
   'moan'          =&gt; 'moans',
);</code></pre>

<p>The more thoroughly you test the bug, the more completely you will fix
it.</p>

<h3>10. Don't Optimize Code--Benchmark It</h3>

<p>If you need a function to remove duplicate elements of an array, it's
natural to think that a "one-liner" like this:</p>

<pre><code>sub uniq { return keys %{ { map {$_=&gt;1} @_ } } }</code></pre>

<p>will be more efficient than two statements:</p>

<pre><code>sub uniq {
   my %seen;
   return grep {!$seen{$_}++} @_;
}</code></pre>

<p>Unless you are deeply familiar with the internals of the Perl interpreter
(in which case you already have far more serious personal issues to deal with),
intuitions about the relative performance of two constructs are exactly that:
unconscious guesses.</p>

<p>The only way to know for sure which of two--or more--alternatives will perform
better is to actually time each of them. The standard <a
href="http://search.cpan.org/perldoc?Benchmark">Benchmark</a> module makes that
easy:</p>

<pre><code># A short list of not-quite-unique values...
our @data = qw( do re me fa so la ti do );

# Various candidates...
sub unique_via_anon {
   return keys %{ { map {$_=&gt;1} @_ } };
}

sub unique_via_grep {
   my %seen;
   return grep { !$seen{$_}++ } @_;
}

sub unique_via_slice {
   my %uniq;
   @uniq{@_} = ();
   return keys %uniq;
}

# Compare the current set of data in @data
sub compare {
   my ($title) = @_;
   print "\n[$title]\n";

   # Create a comparison table of the various timings, making sure that
   # each test runs at least 10 CPU seconds...
   use Benchmark qw( cmpthese );
   cmpthese -10, {
       anon  =&gt; 'my @uniq = unique_via_anon(@data)',
       grep  =&gt; 'my @uniq = unique_via_grep(@data)',
       slice =&gt; 'my @uniq = unique_via_slice(@data)',
   };

   return;
}

compare('8 items, 10% repetition');

# Two copies of the original data...
@data = (@data) x 2;
compare('16 items, 56% repetition');

# One hundred copies of the original data...
@data = (@data) x 50;
compare('800 items, 99% repetition');</code></pre>

<p>The <code>cmpthese()</code> subroutine takes a number, followed by a
reference to a hash of tests. The number specifies either the exact number of
times to run each test (if the number is positive), or the absolute number of
CPU seconds to run the test for (if the number is negative). Typical values are
around 10,000 repetitions or ten CPU seconds, but the module will warn you if
the test is too short to produce an accurate benchmark.</p>

<p>The keys of the test hash are the names of your tests, and the corresponding
values specify the code to be tested. Those values can be either strings (which
are <code>eval</code>'d to produce executable code) or subroutine references
(which are called directly).</p>

<p>The benchmarking code shown above would print out something like the
following:</p>

<pre><code>[8 items, 10% repetitions]
        Rate anon  grep slice
anon  28234/s --  -24%  -47%
grep  37294/s   32% --  -30%
slice 53013/s   88% 42%    --

[16 items, 50% repetitions]
        Rate anon  grep slice
anon  21283/s --  -28%  -51%
grep  29500/s   39% --  -32%
slice 43535/s  105% 48%    --

[800 items, 99% repetitions]
       Rate  anon grep slice
anon   536/s --  -65%  -89%
grep  1516/s  183% --  -69%
slice 4855/s  806%  220% --</code></pre>

<p>Each of the tables printed has a separate row for each named test. The
first column lists the absolute speed of each candidate in repetitions per
second, while the remaining columns allow you to compare the relative
performance of any two tests. For example, in the final test tracing across
the <code>grep</code> row to the <code>anon</code> column reveals that the
<code>grep</code>ped solution was 1.83 times (183 percent) faster than using an anonymous hash.
Tracing further across the same row also indicates that <code>grep</code>ping was 69 percent
slower (-69 percent faster) than slicing.</p>

<p>Overall, the indication from the three tests is that the slicing-based
solution is consistently the fastest for this particular set of data on this
particular machine. It also appears that as the data set increases in size,
slicing also scales much better than either of the other two approaches.</p>

<p>However, those two conclusions are effectively drawn from only three data
points (namely, the three benchmarking runs). To get a more definitive
comparison of the three methods, you'd also need to test other possibilities,
such as a long list of non-repeating items, or a short list with nothing but
repetitions.</p>

<p>Better still, test on the real data that you'll actually be
"unique-ing."</p>

<p>For example, if that data is a sorted list of a quarter of a million words, with
only minimal repetitions, and which has to remain sorted, then test exactly
that:</p>

<pre><code>our @data = slurp '/usr/share/biglongwordlist.txt';

use Benchmark qw( cmpthese );

cmpthese 10, {
    # Note: the non-grepped solutions need a post-uniqification re-sort
    anon  =&gt; 'my @uniq = sort(unique_via_anon(@data))',
    grep  =&gt; 'my @uniq = unique_via_grep(@data)',
    slice =&gt; 'my @uniq = sort(unique_via_slice(@data))',
};</code></pre>

<p>Not surprisingly, this benchmark indicates that the <code>grep</code>ped solution is
markedly superior on a large sorted data set:</p>

<pre><code>s/iter anon slice  grep
anon    4.28 --   -3%  -46%
slice   4.15 3%    --  -44%
grep    2.30 86%   80%    --</code></pre>

<p>Perhaps more interestingly, the <code>grep</code>ped solution still benchmarks as being
marginally faster when the two hash-based approaches aren't re-sorted. This
suggests that the better scalability of the sliced solution as seen in the
earlier benchmark is a localized phenomenon, and is eventually undermined by
the growing costs of allocation, hashing, and bucket-overflows as the sliced
hash grows very large.</p>

<p>Above all, that last example demonstrates that benchmarks only benchmark the
cases you actually benchmark, and that you can only draw useful conclusions
about performance from benchmarking real data.</p>




        </div>



    </div>
    <div class="asset-footer"></div>
</div>


                            
                            <div id="entry-1200" class="entry-asset asset hentry">
    <div class="asset-header">
        <h2 class="asset-name entry-title"><a href="/pub/2003/10/refactoring.html" rel="bookmark">A Refactoring Example</a></h2>
        <div class="asset-meta">
            <span class="byline">
    
                By <span class="vcard author">Michael Schwern</span> on <abbr class="published" title="2003-10-09T00:00:00-08:00">October  9, 2003 12:00 AM</abbr>
    
            </span>

            
            

        </div>
    </div>
    <div class="asset-content entry-content">

        <div class="asset-body">
            
<!-- sidebar begins -->
<!-- don't move sidebars -->
<!-- sidebar ends -->

<p>
About a year ago, a person asked the <a href="http://www.technofile.org/technofile/depts/mlists/fwp.html">Fun With Perl</a> mailing list about some
code they had written to do database queries.  It's important to note that this person was posting from an .it address; why will become apparent later.  The code was reading records
in from a text file and then doing a series of queries based on that
information.  They wanted to make it faster.
</p>


<p>
Here's his code:
</p>


<pre><code>open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($riga=&lt;INPUT&gt;){
$nump++;
chop($riga);
$pagina[$nump] = $riga;

$sth= $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
pageid='$pagina[$nump]' and data&gt;='$startdate'");
$sth-&gt;execute;
$totalvisit[$nump] = $sth-&gt;fetchrow_array();

$sth = $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
(pageid='$pagina[$nump]' and data='$dataoggi')");
$sth-&gt;execute;
$totalvisittoday[$nump] = $sth-&gt;fetchrow_array();

 $sth = $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
(pageid='$pagina[$nump]' and data='$dataieri')");
$sth-&gt;execute;
$totalyvisit[$nump] = $sth-&gt;fetchrow_array();

 $sth= $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
(pageid='$pagina[$nump]' and data&lt;='$fine30gg' and data&gt;='$inizio30gg')");
$sth-&gt;execute;
$totalmvisit[$nump] = $sth-&gt;fetchrow_array();

 }

</code></pre>
<p>
I decided that rather than try to read through this code and figure
out what it's doing and how to make it faster, I'd clean it up first.
Clean it up <b>before</b> you figure out how it works?  Yes, using a
technique called <em>Refactoring</em>.
</p>



<h3>Refactoring?</h3>
<p>

In his book, Martin Fowler defines Refactoring as <em>"the process of changing a software system in such a way that it does not alter the external behavior of the code yet improves its internal structure."</em>
In other words, you clean up your code but don't change what it does.
</p>


<p>
Refactoring can be as simple as changing this code:
</p>


<pre><code>$exclamation = 'I like '.$pastry.'!';

</code></pre>
<p>
To this:
</p>


<pre><code>$exclamation = "I like $pastry!";

</code></pre>
<p>
Still does the same thing, but it's easier to read.
</p>


<p>
It's important to note that I don't need to know anything about the
contents of <code>$pastry</code> or how <code>$exclamation</code> is used.  The change is
completely self-contained and does not affect surrounding code or
change what it does.  This is Refactoring.
</p>


<p>
On the principle of "show me don't tell me," rather than talk about it,
we'll dive right into refactoring our bit of code.
</p>



<h3>Fix the Indentation</h3>
<p>

Your first impulse when faced with a hunk of code slammed against the
left margin is to indent it.  This is our first refactoring.
</p>


<pre><code>open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($riga=&lt;INPUT&gt;){
<font color="#808000">    $nump++;
    chop($riga);
    $pagina[$nump] = $riga;

    $sth= $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
                         pageid='$pagina[$nump]' and data&gt;='$startdate'");
    $sth-&gt;execute;
    $totalvisit[$nump] = $sth-&gt;fetchrow_array();

    $sth = $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
                          (pageid='$pagina[$nump]' and data='$dataoggi')");
    $sth-&gt;execute;
    $totalvisittoday[$nump] = $sth-&gt;fetchrow_array();

    $sth = $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
                          (pageid='$pagina[$nump]' and data='$dataieri')");
    $sth-&gt;execute;
    $totalyvisit[$nump] = $sth-&gt;fetchrow_array();

    $sth= $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
                         (pageid='$pagina[$nump]' and data&lt;='$fine30gg' 
						 and data&gt;='$inizio30gg')");
    $sth-&gt;execute;
    $totalmvisit[$nump] = $sth-&gt;fetchrow_array();
}</font>

close (INPUT);

</code></pre>
<p>
Already it looks better.  We can see that we're iterating over a file,
performing some SELECTs on each line and shoving the results into a
bunch of arrays.
</p>



<h3>A Single, Simple Change</h3>
<p>

One of the most important principles of Refactoring is that you work
in small steps.  This re-indentation is a single step.  And part of this single step includes running the test suite, logging the change, and checking it into CVS.
</p>


<p>
Checking into CVS after something this simple?  Yes.  Many programmers
ask the question, "When should I check in?"  When you're refactoring
it's simple: check in when you've done one refactoring and have tested that it
works.  Our re-indentation is one thing; we test that it works and
check it in.
</p>


<p>
This may seem excessive, but it prevents us from entangling two
unrelated changes together.  By doing one change at a time we know
that any new bugs were introduced by that one change.  Also, you will
often decide in the middle of a refactoring that it's not such a good
idea.  When you've checked in at every one you can simply rollback to
the last version rather than having to undo it by hand.  Convenient,
and you're sure no stray bits of your aborted change are hanging around.
</p>


<p>
So our procedure for doing a proper refactoring is:
</p>


<ul>
<li>Make one logical change to the code.</li>
<li>Make sure it passes tests.</li>
<li>Log and check in.</li>
</ul>


<h3>Big Refactorings from Small</h3>
<p>

The goal of this refactoring is to make the code go faster.  One of
the simplest ways to do achieve that is to pull necessary code out of
the loop.  Preparing four new statements in every iteration of the
loop seems really unnecessary.  We'd like to pull those
<code>prepare()</code>
statements out of the loop.  This is a refactoring.  To achieve this
larger refactoring, a series of smaller refactorings must be done.
</p>



<h3>Use Bind Variables</h3>
<p>

Each time through the loop, a new set of SQL statements is created
based on the line read in.  But they're all basically the same, just
the data is changing.  If we could pull that data out of the statement
we'd be closer to our goal of pulling the <code>prepare()</code>s out of the loop.
</p>


<p>
So my next refactoring pulls variables out of the SQL statements and
replaces them with placeholders.  Then the data is bound to the
statement using bind variables.  This means we're now
<code>prepare()</code>ing the
same statements every time through the loop.
</p>


<p>
Before refactoring:
</p>


<pre><code>$sth= $dbh-&gt;prepare("SELECT count(*) FROM lognew WHERE
                     pageid='$pagina[$nump]' and data&gt;='$startdate'");
$sth-&gt;execute;

</code></pre>
<p>
After refactoring:
</p>


<pre><code>$sth= $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE 
                     pageid=<font color="#808000">?</font> and data&gt;=<font color="#808000">?</font>');
$sth-&gt;execute<font color="#808000">($pagina[$nump], $startdate)</font>;

</code></pre>
<p>
Bind variables also protect against a naughty user from trying to slip
some extra SQL into your program via the data you read in.  As a
side-effect of our code cleanup, we've closed a potential security
hole.
</p>


<pre><code>open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($riga=&lt;INPUT&gt;){
    $nump++;
    chop($riga);
    $pagina[$nump] = $riga;

    $sth= $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE 
                         pageid=<font color="#808000">?</font> and data&gt;=<font color="#808000">?</font>');
    $sth-&gt;execute<font color="#808000">($pagina[$nump], $startdate)</font>;
    $totalvisit[$nump] = $sth-&gt;fetchrow_array();

    $sth = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                          (pageid=<font color="#808000">?</font> and data=<font color="#808000">?</font>)');
    $sth-&gt;execute<font color="#808000">($pagina[$nump], $dataoggi)</font>;
    $totalvisittoday[$nump] = $sth-&gt;fetchrow_array();

    $sth = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                          (pageid=<font color="#808000">?</font> and data=<font color="#808000">?</font>)');
    $sth-&gt;execute<font color="#808000">($pagina[$nump], $dataieri)</font>;
    $totalyvisit[$nump] = $sth-&gt;fetchrow_array();

    $sth= $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                         (pageid=<font color="#808000">?</font> and data&lt;=<font color="#808000">?</font> and data&gt;=<font color="#808000">?</font>)');
    $sth-&gt;execute<font color="#808000">($pagina[$nump], $fine30gg, $inizio30gg)</font>;
    $totalmvisit[$nump] = $sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Split a Poorly Reused Variable</h3>
<p>

The next stumbling block to pulling the <code>prepare()</code> statements out of
the loop is that they all use the same variable, <code>$sth</code>.  We'll have
to change it so they all use different variables.  While we're at it,
we'll name those statement handles something more descriptive of what
the statement does.  Since at this point we haven't figured out what
the statements do, we can base the name on the array it gets assigned
to.
</p>


<p>
While we're at it, throw in some <code>my()</code> declarations to limit the scope
of these variables to just the loop.
</p>


<pre><code>open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($riga=&lt;INPUT&gt;){
    $nump++;
    chop($riga);
    $pagina[$nump] = $riga;

    <font color="#808000">my $totalvisit_sth</font> = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE 
                                        pageid=? and data&gt;=?');
    <font color="#808000">$totalvisit_sth</font>-&gt;execute($pagina[$nump], $startdate);
    $totalvisit[$nump] = <font color="#808000">$totalvisit_sth</font>-&gt;fetchrow_array();

    <font color="#808000">my $totalvisittoday_sth</font> = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                                             (pageid=? and data=?)');
    <font color="#808000">$totalvisittoday_sth</font>-&gt;execute($pagina[$nump], $dataoggi);
    $totalvisittoday[$nump] = <font color="#808000">$totalvisittoday_sth</font>-&gt;fetchrow_array();

    <font color="#808000">my $totalyvisit_sth</font> = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                                         (pageid=? and data=?)');
    <font color="#808000">$totalyvisit_sth</font>-&gt;execute($pagina[$nump], $dataieri);
    $totalyvisit[$nump] = <font color="#808000">$totalyvisit_sth</font>-&gt;fetchrow_array();

    <font color="#808000">my $totalmvisit_sth</font>= $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                                        (pageid=? and data&lt;=? and data&gt;=?)');
    <font color="#808000">$totalmvisit_sth</font>-&gt;execute($pagina[$nump], $fine30gg, $inizio30gg);
    $totalmvisit[$nump] = <font color="#808000">$totalmvisit_sth</font>-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>













<h3>Getting Better All the Time</h3>
<p>

The new names are better, but they're not great.  This is ok.  Naming is
something people often get hung up on.  One can spend hours wracking
their brains thinking of the perfect name for a variable or a
function.  If you can think of a better one than what's there right
now, use it.  The beauty of Refactoring is you an always improve upon
it later.
</p>


<p>
This is an important lesson of Refactoring.  Voltare said, "the best
is the enemy of the good".  We often get so wound up trying to make
code <em>great</em> that we fail to improve it at all.  In refactoring,
it's
not so important to make your code great in one leap, just a little
better all the time (it's a little known fact John Lennon was into
Refactoring.)  These small improvements will build up into a clean
piece of code, with less bugs, more surely than a large-scale code
cleanup would.
</p>



<h3>Pull Code Out of the Loop</h3>
<p>

Now it's a simple cut and paste to pull the four <code>prepare()</code> statements
out of the loop.
</p>



<pre><code><font color="#808000">my $totalvisit_sth = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE 
                                    pageid=? and data&gt;=?');

my $totalvisittoday_sth = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                                         (pageid=? and data=?)');

my $totalyvisit_sth = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                                     (pageid=? and data=?)');

my $totalmvisit_sth = $dbh-&gt;prepare('SELECT count(*) FROM lognew WHERE
                                     (pageid=? and data&lt;=? and data&gt;=?)');
</font>
open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($riga=&lt;INPUT&gt;){
    $nump++;
    chop($riga);
    $pagina[$nump] = $riga;

    $totalvisit_sth-&gt;execute($pagina[$nump], $startdate);
    $totalvisit[$nump] = $totalvisit_sth-&gt;fetchrow_array();

    $totalvisittoday_sth-&gt;execute($pagina[$nump], $dataoggi);
    $totalvisittoday[$nump] = $totalvisittoday_sth-&gt;fetchrow_array();

    $totalyvisit_sth-&gt;execute($pagina[$nump], $dataieri);
    $totalyvisit[$nump] = $totalyvisit_sth-&gt;fetchrow_array();

    $totalmvisit_sth-&gt;execute($pagina[$nump], $fine30gg, $inizio30gg);
    $totalmvisit[$nump] = $totalmvisit_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Already the code is looking better.  With the SQL separated, the inner
workings of the loop are much less daunting.
</p>


<p>
Test.  Log.  Check in.
</p>



<h3>A Place to Stop</h3>
<p>

Remember our goal, to make this code run faster.  By pulling the
<code>prepare()</code> statements outside the loop we've likely achieved this goal.
Additionally, it still does exactly what it did before even though we
still don't fully understand what that is.  If this were a real
project, you'd do some benchmarking to see if the code is fast enough
and move on to another task.
</p>


<p>
Since this is an example, I'll continue with more refactorings with
the goal of clarifying the code further and figuring out what it does.
</p>


<p>
Keep in mind that after every refactoring the code still does <i>exactly
what it did before</i>.  This means we can stop choose to stop after any
refactoring.  If a more pressing task suddenly pops up we can pause
our refactoring work and attend to that feeling confident we didn't
leave any broken code lying around.
</p>



<h3>Reformat SQL for Better Readability</h3>
<p>

In order to make sense of the code, we have to make sense of the SQL.
The simplest way to better understand the SQL is to put it into a
clearer format.
</p>


<p>
The three major parts of an SQL SELECT statement are:
</p>


<ul>
<li>The rows (ie. <code>SELECT count(*)</code>)</li>
<li>The table (ie. <code>FROM lognew</code>)</li>
<li>The predicate (ie. <code>WHERE pageid = ...</code>)</li>
</ul>
<p>

I've chosen a new format that highlights these parts.
</p>


<p>
I've also removed some unnecessary parenthesis because they just serve to
clutter things up rather than disambiguate an expression.
</p>


<p>
I've also decided to change the quoting style from single quotes to a
here-doc.  It would have also been okay to use <code>q{}</code>.
</p>


<pre><code>my $totalvisit_sth = $dbh-&gt;prepare(<font color="#808000">&lt;&lt;'SQL'</font>);
<font color="#808000">SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL</font>

my $totalvisittoday_sth = $dbh-&gt;prepare(<font color="#808000">&lt;&lt;'SQL'</font>);
<font color="#808000">SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL</font>

my $totalyvisit_sth = $dbh-&gt;prepare(<font color="#808000">&lt;&lt;'SQL'</font>);
<font color="#808000">SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL</font>

my $totalmvisit_sth = $dbh-&gt;prepare(<font color="#808000">&lt;&lt;'SQL'</font>);
<font color="#808000">SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &lt;= ? AND 
       data   &gt;= ?
SQL</font>

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($riga=&lt;INPUT&gt;){
    $nump++;
    chop($riga);
    $pagina[$nump] = $riga;

    $totalvisit_sth-&gt;execute($pagina[$nump], $startdate);
    $totalvisit[$nump] = $totalvisit_sth-&gt;fetchrow_array();

    $totalvisittoday_sth-&gt;execute($pagina[$nump], $dataoggi);
    $totalvisittoday[$nump] = $totalvisittoday_sth-&gt;fetchrow_array();

    $totalyvisit_sth-&gt;execute($pagina[$nump], $dataieri);
    $totalyvisit[$nump] = $totalyvisit_sth-&gt;fetchrow_array();

    $totalmvisit_sth-&gt;execute($pagina[$nump], $fine30gg, $inizio30gg);
    $totalmvisit[$nump] = $totalmvisit_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Remove Redundancy</h3>
<p>

With the SQL in a more readable format, some commonalities become clear.
</p>


<ul>
<li>All the statements are doing a <code>count(*)</code>.</li>
<li>They're all using the <code>lognew</code> table</li>
<li>They're all looking for a certain <code>pageid</code>.</li>
</ul>
<p>

In fact, <code>$totalvisittoday_sth</code> and <code>$totalyvisit_sth</code> are exactly
the same!  Let's eliminate one of them, doesn't matter which, we're
going to rename them in a moment anyway.  <code>$totalyvisit_sth</code> goes
away, making sure to change all references to it to <code>$totalvisittoday_sth</code>.
</p>


<pre><code>my $totalvisit_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $totalvisittoday_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $totalmvisit_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &lt;= ? AND 
       data   &gt;= ?
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($riga=&lt;INPUT&gt;){
    $nump++;
    chop($riga);
    $pagina[$nump] = $riga;

    $totalvisit_sth-&gt;execute($pagina[$nump], $startdate);
    $totalvisit[$nump] = $totalvisit_sth-&gt;fetchrow_array();

    $totalvisittoday_sth-&gt;execute($pagina[$nump], $dataoggi);
    $totalvisittoday[$nump] = $totalvisittoday_sth-&gt;fetchrow_array();

    <font color="#808000">$totalvisittoday_sth</font>-&gt;execute($pagina[$nump], $dataieri);
    $totalyvisit[$nump] = <font color="#808000">$totalvisittoday_sth</font>-&gt;fetchrow_array();

    $totalmvisit_sth-&gt;execute($pagina[$nump], $fine30gg, $inizio30gg);
    $totalmvisit[$nump] = $totalmvisit_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>













<h3>Fix Conflicting Styles</h3>
<p>

Now the only difference between the statements is the choice of
<code>data</code> ranges.
</p>


<p>
Using the variables are passed into each statement we can
make some more deductions.  Let's have a look...
</p>


<ul>
<li><code>$startdate</code></li>
<li><code>$dataoggi</code></li>
<li><code>$dataieri</code></li>
<li><code>$fine30gg, $inizio30gg</code></li>
</ul>
<p>

<em>One of these things is not like the other.</em> What's <code>$startdate</code>
doing there?  Everything else is talking about 'data'.  What's 'ieri'?
'oggi'?  Remember, the programmer who submitted this code is Italian.
Maybe the names are in Italian.  Grabbing an <a href="http://dictionaries.travlang.com/ItalianEnglish/">Italian-English dictionary</a> we find
out that 'data' is Italian for 'date'!  Now it makes sense, this code
was probably originally written in English, then worked on by an
Italian (or vice-versa).
</p>


<p>
This code has committed a cardinal stylistic sin.  It uses two
different languages for naming variables.  Not just different
languages, languages which have different meanings for the same words.
Taken out of context, we can't know if <code>$data</code> represents a hunk of
facts or "Thursday."
</p>


<p>
Since the styles conflict, one of them has to go.  Since I don't speak
Italian, I'm going to translate it into English.
</p>


<p>
Pulling out our Italian-to-English dictionary...
</p>


<ul>
<li>"riga" is "line"</li>
<li>"pagina" is "page"</li>
<li>"nump" is probably short for "numero pagina" which is "page number"</li>
<li>"data" is "date"</li>
<li>"oggi" is "today"</li>
<li>"ieri" is "yesterday"</li>
<li>"inizio" is "start"</li>
<li>"fine" is "end"</li>
<li>"gg" is probably short for "giorni" which is "days"
 <ul>
 <li>"fine30gg" would then be "the end of 30 days"</li>
 <li>"inizio30gg" would be "the beginning of 30 days"</li>
 </ul></li>
</ul>
<p>

It would be a straightforward matter of a bunch of search-and-replaces
in any good editor but for one snag, the SQL column 'data.'  We'd like
to change this to its English 'date', but databases are very global
with possibly lots of other programs using it.  So we can't change the
column name without breaking other code. While in a well-organized
programming shop you might have the ability to find all the code which
uses your database, we won't assume we have that luxury here.  For the
moment then, we'll leave that be and deal with it in a separate
refactoring.
</p>


<pre><code>my $totalvisit_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $totalvisittoday_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $totalmvisit_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &lt;= ? AND 
       data   &gt;= ?
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($<font color="#808000">line</font>=&lt;INPUT&gt;){
    $<font color="#808000">page_num</font>++;
    chop($<font color="#808000">line</font>);
    $<font color="#808000">pages</font>[$<font color="#808000">page_num</font>] = $<font color="#808000">line</font>;

    $totalvisit_sth-&gt;execute($<font color="#808000">pages</font>[$<font color="#808000">page_num</font>], $<font color="#808000">start_date</font>);
    $totalvisit[$<font color="#808000">page_num</font>] = $totalvisit_sth-&gt;fetchrow_array();

    $totalvisittoday_sth-&gt;execute($<font color="#808000">pages</font>[$<font color="#808000">page_num</font>], $<font color="#808000">today</font>);
    $totalvisittoday[$<font color="#808000">page_num</font>] = $totalvisittoday_sth-&gt;fetchrow_array();

    $totalvisittoday_sth-&gt;execute($<font color="#808000">pages</font>[$<font color="#808000">page_num</font>], $<font color="#808000">yesterday</font>);
    $totalyvisit[$<font color="#808000">page_num</font>] = $totalvisittoday_sth-&gt;fetchrow_array();

    $totalmvisit_sth-&gt;execute($<font color="#808000">pages</font>[$<font color="#808000">page_num</font>], $<font color="#808000">end_of_30_days</font>,
                                                 $<font color="#808000">start_of_30_days</font>);
    $totalmvisit[$<font color="#808000">page_num</font>] = $totalmvisit_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Better Names</h3>
<p>

With decent variable names in place, the purpose of the program
becomes <b>much</b> clearer.  This is a program to calculate the number of
visits to a page for various date ranges.  Based on this new
information we can give the statement handles and the arrays they put
data into better names.
</p>


<p>
Looking at the SQL we see we've got:
</p>


<ul>
<li>One to get all the visits up to a single day.</li>
<li>One to get the visits for a certain date.</li>
<li>One to get the visits for a range of dates.</li>
</ul>
<p>

A good set of new names would be:
</p>


<ul>
<li>daily</li>
<li>up to</li>
<li>range</li>
</ul>
<p>

Also, Total Visits is too long.  We could shorten that to just
Visits, or even shorter to Hits.
</p>


<pre><code>my $<font color="#808000">hits_upto</font>_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $<font color="#808000">hits_daily</font>_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $<font color="#808000">hits_range</font>_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &lt;= ? AND 
       data   &gt;= ?
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($line=&lt;INPUT&gt;){
    $page_num++;
    chop($line);
    $pages[$page_num] = $line;

    $<font color="#808000">hits_upto</font>_sth-&gt;execute($pages[$page_num], $start_date);
    $totalvisit[$page_num] = $<font color="#808000">hits_upto</font>_sth-&gt;fetchrow_array();

    $<font color="#808000">hits_daily</font>_sth-&gt;execute($pages[$page_num], $today);
    $totalvisittoday[$page_num] = $<font color="#808000">hits_daily</font>_sth-&gt;fetchrow_array();

    $<font color="#808000">hits_daily</font>_sth-&gt;execute($pages[$page_num], $yesterday);
    $totalyvisit[$page_num] = $<font color="#808000">hits_daily</font>_sth-&gt;fetchrow_array();

    $<font color="#808000">hits_range</font>_sth-&gt;execute($pages[$page_num], $end_of_30_days,
                                                $start_of_30_days);
    $totalmvisit[$page_num] = $<font color="#808000">hits_range</font>_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Changing Global Variable Names</h3>
<p>

The array names need work, too.  Currently, they're rather ambiguous.
<code>@totalyvisit</code>, what does the <i>y</i> mean?  Looking at each variable
name and the variables that got passed to <code>execute()</code> to produce it...
</p>


<ul>
<li><code>@totalvisit</code> comes up to a <code>$start_date</code>.  So that can be <code>@hits_upto</code></li>
<li><code>@totalvisittoday</code> comes from <code>$today</code> and is pretty obvious.  <code>@hits_today</code></li>
<li><code>@totalyvisit</code> comes from <code>$yesterday</code> so 'y' must be for 'yesterday'.  <code>@hits_yesterday</code></li>
<li><code>@totalmvisit</code> comes from the range produced by the $start_of_30_days and the $end_of_30_days.  So 'm' must be 'month'.  <code>@hits_monthly</code></li>
</ul>
<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &lt;= ? AND 
       data   &gt;= ?
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($line=&lt;INPUT&gt;){
    $page_num++;
    chop($line);
    $pages[$page_num] = $line;

    $hits_upto_sth-&gt;execute($pages[$page_num], $start_date);
    $<font color="#808000">hits_upto</font>[$page_num] = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($pages[$page_num], $today);
    $<font color="#808000">hits_today</font>[$page_num] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($pages[$page_num], $yesterday);
    $<font color="#808000">hits_yesterday</font>[$page_num] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($pages[$page_num], $end_of_30_days,
                                                $start_of_30_days);
    $<font color="#808000">hits_monthly</font>[$page_num] = $hits_range_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test... uh-oh, test failed!
</p>


<p>
There's something <b>very</b> different about this change compared to the
others.  The variables we changed were <em>not</em> declared in our little
code block.  Likely they're used in other parts of the code, such as
our test which caused it to break.
</p>


<p>
In the Real World, we would be sure to <b>replace all occurrences of the
variable</b>.  The simplest way to do this is to use your editor to
perform a search and replace rather than doing it by your all too
fallible hands.  If it could be used over a set of files, grepping
through those files for all occurrences of it and changing those as
well would be necessary.
</p>


<pre><code># If you don't have rgrep, grep -r does the same thing.
rgrep '[@$]totalvisit' /path/to/your/project

</code></pre>
<p>
I do this so often that I've taken to calling grep -r, 'Refactoring
Grep'.  Other languages who's syntax is -- ummm -- not as inspired as
Perl's, such as Java, C++ and Python, have tools for doing this sort
of thing automatically.  Because of the complexity of Perl's syntax, we
still have to do it mostly by hand, though there are some efforts
underway to rectify this. 
</p>


<p>
Changing the array names in our test as well we get them to pass.
</p>


<p>
Log.  Check in.
</p>













<h3>Improve Overly Generic Names</h3>
<p>

Continuing with our variable name improvements, we're left with the
last few unimproved names.  Let's start with <code>$line</code>.
</p>


<p>
Since we can see clearly that <code>$line = &lt;INPUT&gt;</code>, calling the variable
'line' tells us nothing new.  A better name might be what each line
contains.  Looking at how the line is used we see <code>$pages[$page_num]
= $line</code> and how that is then used in the SQL.  It's a page id.
</p>


<p>
But it doesn't make much sense to put a page id into an array called
<code>@pages</code>.  It doesn't contain pages, it contains <code>@page_ids</code>.
</p>


<p>
What about <code>$page_num</code>?  It doesn't contain a page number, it contains
the line number of the file we're reading in.  Or more conventionally,
an <code>$index</code> or <code>$idx</code>.
</p>


<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &lt;= ? AND 
       data   &gt;= ?
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($<font color="#808000">page_id</font>=&lt;INPUT&gt;){
    $<font color="#808000">idx++</font>;
    chop($<font color="#808000">page_id</font>);
    $<font color="#808000">page_ids</font>[$idx] = $<font color="#808000">page_id</font>;

    $hits_upto_sth-&gt;execute($<font color="#808000">page_ids</font>[$<font color="#808000">idx</font>], $start_date);
    $hits_upto[$<font color="#808000">idx</font>] = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($<font color="#808000">page_ids</font>[$<font color="#808000">idx</font>], $today);
    $hits_today[$<font color="#808000">idx</font>] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($<font color="#808000">page_ids</font>[$<font color="#808000">idx</font>], $yesterday);
    $hits_yesterday[$<font color="#808000">idx</font>] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($<font color="#808000">page_ids</font>[$<font color="#808000">idx</font>], $end_of_30_days,
                                                   $start_of_30_days);
    $hits_monthly[$<font color="#808000">idx</font>] = $hits_range_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Fixing Odd Interfaces</h3>
<p>

What's wrong with this picture?
</p>


<pre><code>$hits_range_sth-&gt;execute($page_ids[$idx], $end_of_30_days,
                                               $start_of_30_days);

</code></pre>
<p>
Isn't it a little odd to specify a date range with the end first?
Sure is.  It also guarantees someone is going to get it backwards.
Reverse it.  Don't forget the SQL, too.
</p>


<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
<font color="#808000">       data   &gt;= ? AND
       data   &lt;= ? </font>
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($page_id=&lt;INPUT&gt;){
    $idx++;
    chop($page_id);
    $page_ids[$idx] = $page_id;

    $hits_upto_sth-&gt;execute($page_ids[$idx], $start_date);
    $hits_upto[$idx] = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_ids[$idx], $today);
    $hits_today[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_ids[$idx], $yesterday);
    $hits_yesterday[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($page_ids[$idx], <font color="#808000">$start_of_30_days,
                                                   $end_of_30_days</font>);
    $hits_monthly[$idx] = $hits_range_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>s/chop/chomp/</h3>
<p>

Now that we've stared at the code for a while, you might have noticed
the use of <code>chop()</code>.  Using <code>chop()</code> to strip a newline is asking
for portability problems, so let's fix it by using <code>chomp()</code>.
</p>


<p>
Technically this <em>isn't</em> a refactoring since we altered the behavior
of the code by fixing the bug.  But using <code>chop()</code> where you meant
<code>chomp()</code> is such a common mistake we'll make it an honorary
refactoring.
</p>


<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &gt;= ? AND
       data   &lt;= ? 
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($page_id=&lt;INPUT&gt;){
    $idx++;
    <font color="#808000">chomp</font>($page_id);
    $page_ids[$idx] = $page_id;

    $hits_upto_sth-&gt;execute($page_ids[$idx], $start_date);
    $hits_upto[$idx] = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_ids[$idx], $today);
    $hits_today[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_ids[$idx], $yesterday);
    $hits_yesterday[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($page_ids[$idx], $start_of_30_days,
                                                   $end_of_30_days,);
    $hits_monthly[$idx] = $hits_range_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Collect Related Variables into Hashes</h3>
<p>

The common prefix <code>hits_</code> is a dead giveaway that much of the data in
this code is related.  Related variables should be grouped together
into a single structure, probably a hash to make the relation obvious
and allow them to be passed around to subroutines more easily.  Its
easier to move around one hash than four arrays.
</p>


<p>
I've decided to collect together all the <code>@hit_</code> arrays into a single
hash <code>%hits</code> since they'll probably be used together parts of the
program.  If this code snippet represents a function it means I can
return one hash reference rather than four array refs.  It also makes
future expansion easier, rather than returning an additional array it
simply becomes another key in the hash.
</p>


<p>
Before.
</p>


<pre><code>$hits{upto}[$idx] = $hits_upto_sth-&gt;fetchrow_array();

</code></pre>
<p>
After.
</p>


<pre><code>$hits_upto[$idx]  = $hits_upto_sth-&gt;fetchrow_array();

</code></pre>
<p>
It's interesting to note what a small, natural change this is.
Circumstantial evidence that this is a good refactoring.
</p>


<p>
As before, since these arrays are global data, we must be sure to
change them everywhere.  This includes the tests.
</p>


<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &gt;= ? AND
       data   &lt;= ? 
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($page_id=&lt;INPUT&gt;){
    $idx++;
    chomp($page_id);
    $page_ids[$idx] = $page_id;

    $hits_upto_sth-&gt;execute($page_ids[$idx], $start_date);
    $hits<font color="#808000">{upto}</font>[$idx] = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_ids[$idx], $today);
    $hits<font color="#808000">{today}</font>[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_ids[$idx], $yesterday);
    $hits<font color="#808000">{yesterday}</font>[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($page_ids[$idx], $start_of_30_days,
                                                   $end_of_30_days,);
    $hits<font color="#808000">{monthly}</font>[$idx] = $hits_range_sth-&gt;fetchrow_array();
}

close (INPUT);

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>When Not to Refactor</h3>
<p>

The statement handles are also related, but I'm not going to collect
them together into a hash.  The statement handles are short-lived
lexicals, they're never likely to be passed around.  Their short scope
and grouping within the code makes their relationship obvious.  The
design would not be improved by the refactoring.
</p>


<p>
Refactoring is not a set of rules to be slavishly followed, it's a
collection of tools.  And like any other tool you must carefully
consider when and when not to use it.  Since collecting the statement
handles together doesn't improve the design, I won't do it.
</p>













<h3>Eliminate Unnecessary Longhand</h3>
<p>

Boy, we sure use <code>$page_ids[$idx]</code> a lot.  It's the current page
ID.  But don't we have a variable for that?  
</p>


<p>
Replace all the unnecessary array accesses and just use the more
concise and descriptive <code>$page_id</code>.
</p>


<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &gt;= ? AND
       data   &lt;= ? 
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($page_id=&lt;INPUT&gt;){
    $idx++;
    chomp($page_id);
    $page_ids[$idx] = $page_id;

    $hits_upto_sth-&gt;execute($<font color="#808000">page_id</font>, $start_date);
    $hits{upto}[$idx] = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($<font color="#808000">page_id</font>, $today);
    $hits{today}[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($<font color="#808000">page_id</font>, $yesterday);
    $hits{yesterday}[$idx] = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($<font color="#808000">page_id</font>, $start_of_30_days,
                                       $end_of_30_days,);
    $hits{monthly}[$idx] = $hits_range_sth-&gt;fetchrow_array();
}

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Rearrange Data Structures to Fit Their Use</h3>
<p>

Currently, <code>%hits</code> is accessed by the order the page ID was read out
of the file.  Well, that doesn't seem very useful at all.  Its purpose
seems to be for listing the page counts in exactly the same order as
you read them in.  Even then you need to iterate through <code>@page_ids</code>
simultaneously because no where in <code>%hits</code> is the page ID stored.
</p>


<p>
Consider a common operation, looking up the hit counts for a given
page ID.  You have to iterate through the whole list of page IDs to do
it.
</p>


<pre><code>foreach my $idx (0..$#page_ids) {
    if( $page_ids[$idx] eq $our_page_id ) {
        print "Hits for $our_page_id today: $hits{today}[$idx]\n";
        last;
    }
}

</code></pre>
<p>
Cumbersome.  A much better layout would be a hash keyed on the page ID.
</p>


<pre><code>$hits{upto}{$page_id} = $hits_upto_sth-&gt;fetchrow_array();

</code></pre>
<p>
Now we can directly access the data for a given page ID.  If
necessary, we can still list the hits in the same order they were read
in by iterating through <code>@page_ids</code>.
</p>


<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &gt;= ? AND
       data   &lt;= ? 
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($page_id=&lt;INPUT&gt;){
    $idx++;
    chomp($page_id);
    $page_ids[$idx] = $page_id;

    $hits_upto_sth-&gt;execute($page_id, $start_date);
    $hits{upto}<font color="#808000">{$page_id}</font> = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_id, $today);
    $hits{today}<font color="#808000">{$page_id}</font> = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_id, $yesterday);
    $hits{yesterday}<font color="#808000">{$page_id}</font> = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($page_id, $start_of_30_days,
                                       $end_of_30_days,);
    $hits{monthly}<font color="#808000">{$page_id}</font> = $hits_range_sth-&gt;fetchrow_array();
}

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Eliminate Unnecessary Variables</h3>
<p>

Now that <code>%hits</code> is no longer ordered by how it was read in, <code>$idx</code>
isn't used much anymore.  It's only used to stick <code>$page_id</code> onto the
end of <code>@page_ids</code>, but we can do that with <code>push</code>.
</p>


<p>
This is minor but little things build up to cause big messes.
</p>


<pre><code>my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &gt;= ? AND
       data   &lt;= ? 
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($page_id=&lt;INPUT&gt;){
    chomp($page_id);
    <font color="#808000">push @page_ids, $page_id;</font>

    $hits_upto_sth-&gt;execute($page_id, $start_date);
    $hits{upto}{$page_id} = $hits_upto_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_id, $today);
    $hits{today}{$page_id} = $hits_daily_sth-&gt;fetchrow_array();

    $hits_daily_sth-&gt;execute($page_id, $yesterday);
    $hits{yesterday}{$page_id} = $hits_daily_sth-&gt;fetchrow_array();

    $hits_range_sth-&gt;execute($page_id, $start_of_30_days,
                                       $end_of_30_days,);
    $hits{monthly}{$page_id} = $hits_range_sth-&gt;fetchrow_array();
}

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Pull Logical Chunks Out into Functions</h3>
<p>

Our final refactoring is one of the most common and most useful.
</p>


<p>
Let's assume that we need to generate page counts somewhere else in
the code.  Rather than repeat the code to do this, we want to put it
in a subroutine so it can be reused.  One subroutine for each
statement.
</p>


<p>
In order to do this, start by identifying the code that would go into
the routine.
</p>


<pre><code>$hits_upto_sth-&gt;execute($page_id, $start_date);
$hits{upto}{$page_id} = $hits_upto_sth-&gt;fetchrow_array();

</code></pre>
<p>
Then wrap a subroutine around it.
</p>


<pre><code><font color="#808000">sub hits_upto {</font>
    $hits_upto_sth-&gt;execute($page_id, $start_date);
    $hits{upto}{$page_id} = $hits_upto_sth-&gt;fetchrow_array();
<font color="#808000">}</font>

</code></pre>
<p>
Now look at all the variables used.
</p>


<p>
<code>$hits_upto_sth</code> is a global (well, file-scoped lexical) and is
defined entirely outside the function.  We can keep using it in our
subroutine in the same way we are now.
</p>


<p>
<code>$hits{upto}{$page_id}</code> is receiving the result of the calculation.
It contains the return value.  So it goes outside the function to
receive the return value.  Where its assignment was, we put a <code>return</code>.
</p>


<pre><code>sub hits_upto {
    $hits_upto_sth-&gt;execute($page_id, $start_date);
    <font color="#808000">return</font> $hits_upto_sth-&gt;fetchrow_array();
}

</code></pre>
<p>
<code>$page_id</code> and <code>$start_date</code> vary from call to call.  These are our
function arguments.
</p>


<pre><code>sub hits_upto {
    <font color="#808000">my($page_id, $start_date) = @_;</font>
    $hits_upto_sth-&gt;execute($page_id, $start_date);
    return $hits_upto_sth-&gt;fetchrow_array();
}

</code></pre>
<p>
Finally, rename things in a more generic manner.  This is a subroutine
for calculating the number of hits up to a certain date.  Instead of
<code>$start_date</code> which was specific to one calculation, we'd call it
<code>$date</code>.
</p>


<pre><code>sub hits_upto {
    my($page_id, <font color="#808000">$date</font>) = @_;
    $hits_upto_sth-&gt;execute($page_id, <font color="#808000">$date</font>);
    return $hits_upto_sth-&gt;fetchrow_array();
}

</code></pre>
<p>
And that's our new subroutine, does the same thing as the original
code.  Then it's a simple matter to use it in the code.
</p>

   
<pre><code>    $hits{upto}{$page_id} = <font color="#808000">hits_upto($page_id, $start_date)</font>;


my $hits_upto_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*)
FROM   lognew 
WHERE  pageid =  ? AND 
       data   &gt;= ?
SQL

my $hits_daily_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid = ? AND 
       data   = ?
SQL

my $hits_range_sth = $dbh-&gt;prepare(&lt;&lt;'SQL');
SELECT count(*) 
FROM   lognew 
WHERE  pageid =  ? AND
       data   &gt;= ? AND
       data   &lt;= ? 
SQL

open (INPUT, "&lt; $filepageid") || &amp;file_open_error("$filepageid");

while ($page_id=&lt;INPUT&gt;){
    chomp($page_id);
    <font color="#808000">push @page_ids, $page_id;</font>

    $hits{upto}{$page_id}      = <font color="#808000">hits_upto($page_id, $start_date)</font>;
    $hits{today}{$page_id}     = <font color="#808000">hits_daily($page_id, $today)</font>;
    $hits{yesterday}{$page_id} = <font color="#808000">hits_daily($page_id, $yesterday)</font>;
    $hits{monthly}{$page_id}   = <font color="#808000">hits_range($page_id, $start_of_30_days,
                                                        $end_of_30_days,)</font>;
}

sub hits_upto {
    my($page_id, $date) = @_;
    $hits_upto_sth-&gt;execute($page_id, $date);
    return scalar $hits_upto_sth-&gt;fetchrow_array();
}

sub hits_daily {
    my($page_id, $date) = @_;
    $hits_daily_sth-&gt;execute($page_id, $date);
    return scalar $hits_daily_sth-&gt;fetchrow_array();
}

sub hits_range {
    my($page_id, $start, $end) = @_;
    $hits_range_sth-&gt;execute($page_id, $start, $end);
    return scalar $hits_range_sth-&gt;fetchrow_array();
}

</code></pre>
<p>
Test.  Log.  Check in.
</p>



<h3>Undo.</h3>
<p>

Some may balk at putting that small of a snippet of code into a
subroutine like that.  There are definite performance concerns about
adding four subroutine calls to a loop.  But I'm not worried about
that at all.
</p>


<p>
One of the beauties of Refactoring is that it's reversible.
Refactorings don't change how the program works.  We can reverse any
of these refactorings and the code will work exactly the same.  If a
refactoring turns out to be a bad idea, undo it.  Logging each
refactoring in version control makes the job even easier.
</p>


<p>
So if it turns out moving the executes into their own functions causes
a performance problem the change can easily be undone.
</p>



<h3>Done?</h3>
<p>

At this point, things are looking pretty nice.  The code is well
structured, readable, and efficient.  The variables are sensibly named.
The data is organized in a fairly flexible manner.
</p>


<p>
It's good enough.  This is not to say that there's not more that could
be done, but we don't need to.  And Refactoring is about doing as much
redesign as you need instead of what you might need.
</p>



<h3>Refactoring and the Swiss Army Knife</h3>
<p>

As programmers we have a tendency towards over-design.  We like to
design our code to deal with any possible situation that might arise,
since it was hard to change the design later.  This is known as Big
Design Up Front (BDUF).  It's like one of those <a href="http://www.victorinox.com/newsite/en/produkte/neu/inhalt2.cfm?pid=1-6795-XLT">enormous Swiss Army Knives with 50 functions</a>.  Most of the time all you need is <a href="http://www.victorinox.com/newsite/en/produkte/neu/inhalt2.cfm?pid=2-2363">a knife with something to open your beer with and then maybe pick your teeth afterwards</a> but you never know.  
So you over-engineer because it's hard to improve it later.  If it never gets used then a lot of effort has been wasted.
</p>


<p>
Refactoring turns design on its ear.  Now you can continually evolve
your design as needed.  There's no longer a need to write for every
possible situation up front so you can focus on just what you need
right now.  If you need more flexibility later, you can add that
flexibility through refactoring.  It's like having a Swiss Army knife
that you can add tools to as you need them.
</p>



<h3>Further Reference</h3>
<ul>
<li><a href="http://groups.google.com/groups?th=11b4e3caaafb9849&amp;seekm=20021005063711.GE15102%40ool-18b93024.dyn.optonline.net#link1">The original thread on Fun With Perl</a></li>
<li>The <a href="http://www.c2.com/cgi/wiki?WelcomeVisitors">Portland Pattern Repository</a> answers the question -- <a href="http://www.c2.com/cgi/wiki?WhatIsRefactoring">WhatIsRefactoring?</a></li>
<li><a href="http://www.amazon.com/exec/obidos/tg/detail/-/0201485672">The Refactoring Book</a> by <a href="http://www.martinfowler.com">Martin Fowler</a></li>
</ul>
        </div>



    </div>
    <div class="asset-footer"></div>
</div>


                            
                            <div id="entry-1752" class="entry-asset asset hentry">
    <div class="asset-header">
        <h2 class="asset-name entry-title"><a href="/pub/2000/11/repair3x.html" rel="bookmark">Red Flags Return</a></h2>
        <div class="asset-meta">
            <span class="byline">
    
                By <span class="vcard author">Mark-Jason Dominus</span> on <abbr class="published" title="2000-11-28T00:00:00-08:00">November 28, 2000 12:00 AM</abbr>
    
            </span>

            
            

        </div>
    </div>
    <div class="asset-content entry-content">

        <div class="asset-body">
            <!-- <a name="what's wrong with this picture">What's Wrong With This Picture?</a> -->

<p>Astute readers had a number of comments about last week's
<a href="/pub/2000/11/repair3.html"><cite>Program Repair Shop and Red Flags</cite></a> article.</p> 

<h3><a name="control flow puzzle">Control Flow Puzzle</a></h3> 


<p>In the article, I had a section of code that looked like this:</p> 
<pre>       $_ = &lt;INFO&gt; until !defined($_) || /^(\* Menu:|\037)/;
       return @header if !defined($_) || /^\037/;</pre> 
			
<p>I disliked the structure and especially the repeated tests.  I played
with it, changing it to</p> 

<p><table width="180" cellspacing="0" cellpadding="4" align="right" border="1"><tr><td align="center" valign="top" bgcolor="#6699cc">
<font color="#ffffff">Table of Contents</td>
</tr>

<tr>
<td>
<p class="fine">
&#149;<a href="#control flow puzzle">Control Flow Puzzle</a><br />
&#149;<a href="#pattern matching">Pattern Matching</a><br />
&#149;<a href="#synthetic variables">Synthetic Variables</a><br />
&#149;<a href="#send more code">Send More Code</a><br />

</td>
</tr>
</p>
</table>

<pre>
        while (&lt;INFO&gt;) {
          last if /^\* Menu:/;
          return @header if /^\037/;
        }
        return @header unless defined $_;</pre> 
<p>and then used Simon Cozens' suggestion of</p> 
<pre>
        do { 
          $_ = &lt;INFO&gt;; 
          return @header if /^\037/ || ! defined $_ 
        } until /^\* Menu:/ ;</pre> 
		
<p>This still bothered me, because <code>do...until</code> is unusual.
But I was out of time, so that's what I used.</p> 

<p>Readers came up with two interesting alternatives.
Jeff Pinyan suggested:</p> 

<pre>
        while (&lt;INFO&gt;) {
          last if /^\* Menu:/;
          return %header if /^\037/ or eof(INFO);
        }</pre> 
		
<p>This is perfectly straightforward, and the only reason I didn't think
of it was because of my prejudice against <code>eof()</code>.  In the article, I
recommended avoiding <code>eof()</code>, and that's a good rule of thumb.  But
in this case, I think it was probably the wrong way to go.</p> 

<p>After I saw Jeff's solution, I thought more about <code>eof()</code> and tried
to remember what its real problems are.  The conclusion I came to is
that the big problem with <code>eof()</code> occurs when you use it on a
filehandle that is involved in an interactive dialogue, such as a
terminal.</p> 

<p>Consider code like this:</p> 
<pre>
        my ($name, $fav_color);
        print &quot;Enter your name: &quot;;
        chomp($name = &lt;STDIN&gt;);
        unless (eof(STDIN)) {
          print &quot;Enter your favorite color: &quot;;
          chomp($fav_color = &lt;STDIN&gt;);
        }
</pre> 

<p>This seems straightforward, but it doesn't work.  (Try it!)  After 
user enters their name, we ask for <code>eof()</code>.  This tries to read
another character from <code>STDIN</code>, which means that the program is
waiting for user input <i>before</i> printing the second prompt!  The
program hangs forever at the <code>eof</code> test, and the only way it can
continue is if the user clairvoyantly guesses that they are supposed
to enter their favorite color.  If they do that, then the program will
print the prompt and immediately continue.  Not very useful behavior!
And under some circumstances, this can cause deadlock.</p> 

<p>However, in the example program I was discussing, no deadlock is
possible because the information flows in only one direction - from a
file into the program.  So the use of <code>eof()</code> would have been safe.</p> 

<p>Ilya Zakharevich suggested a solution that I like even better:</p> 
<pre>
      while (&lt;INFO&gt;) {
          return do_menu() if /^\* Menu:/;
          last if /^\037/;
      }
      return %header;</pre> 
	  
<p>Here, instead of requiring the loop to fall through to process the
menu, we simply put the menu-processing code into a subroutine and
process it inside the loop.</p> 

<p>Ilya also pointed out that the order of the tests in the original code
is backward:</p> 

<pre>
	return @header if /^\037/ || ! defined $_</pre> 

<p>It should have looked like this:</p> 

<pre>
	return @header if ! defined $_  || /^\037/;</pre> 

<p>Otherwise, we're trying to do a pattern-match operation on a possibly
undefined value.</p> 

<p>Ilya also suggested another alternative:</p> 
<pre>
    READ_A_LINE: {
      return %header if not defined ($_ = &lt;INFO&gt;) or /^\037/;
      redo READ_A_LINE unless /^\* Menu:/;
    }</pre> 
	
<p>Randal Schwartz suggested something similar.  This points out a
possible rule of thumb: When Perl's control-flow constructions don't
seem to be what you want, try decorating a bare block.</p> 

<h3><a name="oops!">Oops!</a></h3> 

<p>I said:</p> <blockquote> now invoke the function like this:

<pre>
	$object = Info_File-&gt;new('camel.info');
</pre> </blockquote>  

<p>Unfortunately, the function in question was named <code>open_info_file</code>,
not <code>new</code>.  The call should have been</p> 

<pre>
	$object = Info_File-&gt;open_info_file('camel.info');</pre> 

<p>I got the call right in my test program (of <em>course</em> I had a test
program!) but then mixed up the name when I wrote the article.  Thanks
to Adam Turoff for spotting this.</p> 

<h3><a name="pattern matching">Pattern Matching</a></h3> 

<p>In the article, I replaced this:</p> 
<pre>
	($info_file) = /File:\s*([^,]*)/;
        ($info_node) = /Node:\s*([^,]*)/;
        ($info_prev) = /Prev:\s*([^,]*)/;
        ($info_next) = /Next:\s*([^,]*)/;
        ($info_up)   = /Up:\s*([^,]*)/;</pre> 
		
<p>With this:</p> 

<pre>
	for my $label (qw(File Node Prev Next Up)) {
          ($header{$label}) = /$label:\s*([^,]*)/;
        }</pre> 
		
<p>Then I complained that Perl must recompile the regex each time
through the loop, five times per node.  Ilya pointed out
the obvious solution:</p> 

<pre>
	 $header{$1} = $2 
	     while /(File|Node|Prev|Next|Up):\s*([^,]*)/g;
</pre> 

<p>I wish I had thought of this, because you can produce it almost
mechanically.  In fact, I think my original code betrays a red flag
itself.  Whenever you have something like this:</p> 

<pre>
	for $item (LIST) {
          something involving m/$item/;
        }</pre> 
		
<p>this is a red flag, and you should consider trying to replace it with
this:</p> 

<pre>
	my $pat = join '|', LIST;
        Something involving m/$pat/o;</pre> 
		
<p>As a simple example, consider this common construction:</p> 

<pre>
	@states = ('Alabama', 'Alaska', ..., 
	           'West Virginia', 'Wyoming');
        $matched = 0;
        for $state (@states) {
          if ($input =~ /$state/) { 
            $matched = 1; last;
          }
        }</pre> 
		
<p>It's more efficient to use this instead:</p> 

<pre>
	my $pat = join '|', @states;
        $matched = ($input =~ /$pat/o);</pre> 
		
<p>Applying this same transformation to the code in my original program
yields Ilya's suggestion.</p> 

<h3><a name="synthetic variables">Synthetic Variables</a></h3> 

<p>My code looked like this:</p> 

<pre>
	while (&lt;INFO&gt;) {
          return 1 if /^\037/;    # end of node, success.
          next unless /^\* \S/;   # skip non-menu-items
          if (/^\* ([^:]*)::/) {  # menu item ends with ::
              $key = $ref = $1;
          } elsif (/^\* ([^:]*):\s*([^.]*)[.]/) {
              ($key, $ref) = ($1, $2);
          } else {
              print STDERR &quot;Couldn't parse menu item\n\t$_&quot;;
              next;
          }
          $info_menu{$key} = $ref;
        }</pre> 
		
<p>Ilya pointed out that in this code, <code>$key</code> and <code>$ref</code> may be synthetic variables.  A synthetic variable isn't intrinsic to
the problem you're trying to solve; rather, they're an artifact of the
way the problem is expressed in a programming language.  I think
<code>$key</code> and <code>$ref</code> are at least somewhat natural, because the problem
statement <em>does</em> include menu items with names that refer to nodes,
and <code>$key</code> is the name of a menu item and <code>$ref</code> is the node it
refers to.  But some people might prefer Ilya's version:</p> 

<pre>
       while (&lt;INFO&gt;) {
           return 1 if /^\037/;        # end of node, success.
           next unless s/^\* (?=\S)//; # skip non-menu-items
           $info_menu{$1} = $1, next if /^([^:]*)::/; 
           $info_menu{$1} = $2, next if /^([^:]*):\s*(.*?)\./;
           print STDERR &quot;Couldn't parse menu item\n\t* $_&quot;;
       }</pre> 
	   
<p>Whatever else you say about it, this reduces the code from eleven
lines to six, which is good.</p> 

<h3><a name="old news">Old News</a></h3> 

<p>Finally, a belated correction.  In the <em>second</em> <a href="/pub/2000/06/commify.html"><cite>Repair Shop and Red Flags Article</cite></a> way back in June, I got the notion that you shouldn't use string
operations on numbers.  While I still think this is good advice, I
then tried to apply it outside of the domain in which it made sense.</p> 

<p>I was trying to transform a number like 12345678 into an array like 
<code>('12', ',', '345', ',', '678')</code>.  After discussing several strategies, all
of which worked, I
ended with the following nonworking code:</p> 

<pre>
	sub convert {
          my ($number) = shift;
          my @result;
          while ($number) {
            push @result, ($number % 1000) , ',';
            $number = int($number/1000);
          }
          pop @result;      # Remove trailing comma
          return reverse @result;
        }</pre> 
		
<p>If you ask this subroutine to convert the number 1009, you get <code>('1',
',', '9')</code>, which is wrong; it should have been <code>(1, ',', '009')</code>.
Many people wrote to point this out; I think Mark Lybrand was
the first.  Oops!  Of course, you can fix this with <code>sprintf</code>, but
really the solutions I showed earlier in the article are better.</p> 

<p>The problem here is that I became too excited about my new idea.  
I still think it's usually a red flag to treat a number like a
string.  But there's an exception:  When you are formatting a number
for output, you <em>have</em> to treat it like a string, because output is
always a string.  I think Charles Knell hit the nail on the head here:</p> 

<blockquote> By inserting commas into the returned value, you ultimately
        treat the number as a string. Why not just give in and admit
        you're working with a string.
</blockquote> 

<p>Thanks, Charles.</p> 

<p>People also complained that the subroutine returns a rather peculiar
list instead of a single scalar, but that was the original author's
decision and I didn't want to tamper with it without being sure why he
had done it that way.  People also took advantage of the opportunity
to send in every bizarre, convoluted way they would think of to
accomplish the same thing (or even a similar thing), often saying
something like this:</p>

 <blockquote> You are doing way too much work!  Why don't you simply use
        this, like everyone else does?
<pre>
	sub commify {
          $_ = shift . '*';
          &quot;nosehair&quot; while s/(.{1,3})\*/*,$1/;
          substr($_,2);
        }
</pre> </blockquote>

<p>I think this just shows that all code is really simple if you already
happen to understand it.</p>

<h3><a name="send more code">Send More Code</a></h3> 

<p>Finally, thanks to everyone who wrote in, especially the people I
didn't mention.  These articles have been quite popular, and I'd like
to continue them.  But that can't happen unless I have code to
discuss.  So if you'd like to see another ``Red Flags'' article, please
consider sending me a 20- to 50-line section of your own code.  If you
do, I won't publish the article without showing it to you beforehand.</p>  



        </div>



    </div>
    <div class="asset-footer"></div>
</div>




                            <div class="content-nav">
                                <a href="/pub/statistics/">&laquo; Statistics</a> |
                                <a href="/pub/">Main Index</a> |
                                <a href="/pub/archives.html">Archives</a>
                                | <a href="/pub/sysadmin/">Sysadmin &raquo;</a>
                            </div>


                        </div>
                    </div>


                    <div id="beta">
    <div id="beta-inner">


    
    <div class="widget-what-is-perl widget">
    <div class="widget-content widget-content-what-is-perl">
       Visit the home of the  Perl programming language: <a href="http://www.perl.org/">Perl.org</a
    </div>
</div>
<div class="widget-find-out-more widget-archives widget">
    <div class="widget-content">
        <ul>
            <li><a href="http://www.perl.org/get.html">Download</a></li>
            <li><a href="http://perldoc.perl.org/">Documentation</a></li>
            <li><a href="http://blogs.perl.org/">Perl Bloggers</a></li>
            <li><a href="http://news.perlfoundation.org/">Foundation News</a></li>
        </ul>
    </div>
</div><div class="widget-tcpc widget">
<h3 class="widget-header">Sponsored by</h3>
    <div class="widget-content">
        <a href="http://training.perl.com/" alt="Perl Training" target="_blank"><img src="/i/tcpc.png" width="150" height="50"></a>
    </div>
</div>

<div class="widget-syndication widget">
    <div class="widget-content">
        <ul>
            <li><img src="/mt-static/images/status_icons/feed.gif" alt="Subscribe to feed" width="9" height="9" /> <a href="/pub/atom.xml">Subscribe to this website's feed</a></li>

        </ul>
    </div>
</div>
<div class="widget-powered widget">
    <div class="widget-content">
        <a href="http://www.movabletype.com/"><img src="/mt-static/images/bug-pbmt-white.png" alt="Powered by Movable Type 5.02" width="120" height="75" /></a>
    </div>
</div>



    </div>
</div>






                </div>
            </div>


            <div id="footer">
    <div id="footer-inner">
        <div id="footer-content">
            <div class="widget-powered widget">
                <div class="widget-content">
                    Powered by <a href="http://www.movabletype.com/" rel="generator">Movable Type Pro</a>
                </div>
            </div>

        </div>
    </div>
</div>



        </div>
    </div>
</body>
</html>
