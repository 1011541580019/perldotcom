<TITLE>Perl Style: Avoid Byte Processing</TITLE>
<H1>Perl Style: Avoid Byte Processing</H1>

<UL>
<LI>C programmers often try to process strings a byte at a time.
Don't do that!  Perl makes it easy to take data in big bites.<LI>Don't use <CODE>getc</CODE>.  Grab the whole line and operate on it all at once.<LI>Even operations traditionally done a char at a time in C, like
lexing, should be done differently.  For example:<PRE>
    @chars = split //, $input;
    while (@chars) {
      $c = shift @chars;
      # State machine;
    }</PRE>
Is far too low level.  Try something more like:<PRE>
    sub parse_expr {
	local $_ = shift;
	my @tokens = ();
	my $paren = 0;
	my $want_term = 1;</PRE>
<PRE>
	while (length) {
	    s/^\s*//;</PRE>
<PRE>
	    if (s/^\(//) {
		return unless $want_term;
		push @tokens, '(';
		$paren++;
		$want_term = 1;
		next;
	    } </PRE>
<PRE>
	    if (s/^\)//) {
		return if $want_term;
		push @tokens, ')';
		if ($paren &lt; 1) {
		    return;
		} 
		--$paren;
		$want_term = 0;
		next;
	    } </PRE>
<PRE>
	    if (s/^and\b//i || s/^&amp;&amp;?//) {
		return if $want_term;
		push @tokens, '&amp;';
		$want_term = 1;
		next;
	    } </PRE>
<PRE>
	    if (s/^or\b//i || s/^\|\|?//) {
		return if $want_term;
		push @tokens, '|';
		$want_term = 1;
		next;
	    } </PRE>
<PRE>
	    if (s/^not\b//i || s/^~// || s/^!//) {
		return unless $want_term;
		push @tokens, '~';
		$want_term = 1;
		next;
	    } </PRE>
<PRE>
	    if (s/^(\w+)//) {
		push @tokens, '&amp;' unless $want_term;
		push @tokens, $1 . '()';
		$want_term = 0;
		next;
	    } </PRE>
<PRE>
	    return;</PRE>
<PRE>
	}
	return &quot;@tokens&quot;;
    }</PRE>
</UL>
<HR><P>Forward to <A HREF="slide24.html">Avoid Symbolic References</A>
<BR>Back to <A HREF="slide22.html">Use foreach() Loops</A>
<BR>Up to <A HREF="slide-index.html">index</A>
<P>
Copyright &copy; 1998, Tom Christiansen
<BR>
All rights reserved.
