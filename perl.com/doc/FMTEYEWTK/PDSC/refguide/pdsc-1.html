<HEAD>
<TITLE>
	    PDSC #1: Lists of Lists
</TITLE>
<LINK REV=made HREF=mailto:tchrist@perl.com>
</HEAD>

<CENTER>
<H1>
	    Recipes for PDSC #1:<BR> Lists of Lists
</H1>

<b><font size=+1>
<I>
		    by Tom Christiansen 
</I>
		    <BR>
&lt <A HREF=mailto:tchrist@perl.com>tchrist@perl.com</A> &gt
</font>
		    <P>
		    release 0.1 (untested, may have typos)
		    <BR>
		    Sunday, 1 October 1995
</b>

<HR NOSHADE WIDTH="75%">

<font size=+1>
<h2>Note</h2>
Please see the <A HREF="pdsc-1-verbose.html">expanded version</a> of this document
for a much more elaborate description of the recipes used herein.
</font>

</CENTER>


<HR NOSHADE WIDTH="100%">

<H2>Declaration of a LIST OF LISTS:</H2>
<PRE>
@LoL = ( 
       [ "fred", "barney" ],
       [ "george", "jane", "elroy" ],
       [ "homer", "marge", "bart" ],
     );
</PRE>
<H2>Generation of a LIST OF LISTS:</H2>
<PRE>
# reading from file
while ( <> ) {
    push @LoL, [ split ];
}

# calling a function 
for $i ( 1 .. 10 ) {
    $LoL[$i] = [ somefunc($i) ];
}

# using temp vars
for $i ( 1 .. 10 ) {
    @tmp = somefunc($i);
    $LoL[$i] = [ @tmp ];
}

# add to an existing row
push @{ $LoL[0] }, "wilma", "betty";
</PRE>
<H2>Access and Printing of a LIST OF LISTS:</H2>
<PRE>
# one element
$LoL[0][0] = "Fred";

# another element
$LoL[1][1] =~ s/(\w)/\u$1/;

# print the whole thing with refs
for $aref ( @LoL ) {
    print "\t [ @$aref ],\n";
}

# print the whole thing with indices
for $i ( 0 .. $#LoL ) {
    print "\t [ @{$LoL[$i]} ],\n";
}

# print the whole thing one at a time
for $i ( 0 .. $#LoL ) {
    for $j ( 0 .. $#{$LoL[$i]} ) {
	print "elt $i $j is $LoL[$i][$j]\n";
    }
}
</PRE>
