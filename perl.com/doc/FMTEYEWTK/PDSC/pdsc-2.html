<HEAD>
<TITLE>
	    PDSC #2: Hashes of Lists
</TITLE>
<LINK REV=made HREF=mailto:tchrist@perl.com>
</HEAD>

<CENTER>
<H1>
	    Recipes for PDSC #2: Hashes of Lists
</H1>

<b><font size=+1>
<I>
		    by Tom Christiansen 
</I>
		    <BR>
&lt; <A HREF=mailto:tchrist@perl.com>tchrist@perl.com</A> &gt;
</font>
		    <P>
		    release 0.1 (untested, may have typos)
		    <BR>
		    Sunday, 1 October 1995
</b></CENTER>

<BODY>


<HR>

<H2>Declaration of a HASH OF LISTS:</H2>
<PRE>
%HoL = ( 
       "flintstones"        => [ "fred", "barney" ],
       "jetsons"            => [ "george", "jane", "elroy" ],
       "simpsons"           => [ "homer", "marge", "bart" ],
     );
</PRE>
<H2>Generation of a HASH OF LISTS:</H2>
<PRE>
# reading from file
# flintstones: fred barney wilma dino
while ( <> ) {
    next unless s/^(.*?):\s*//;
    $HoL{$1} = [ split ];
}

# reading from file; more temps
# flintstones: fred barney wilma dino
while ( $line = <> ) {
    ($who, $rest) = split /:\s*/, $line, 2;
    @fields = split ' ', $rest;
    $HoL{$who} = [ @fields ];
}

# calling a function that returns a list
for $group ( "simpsons", "jetsons", "flintstones" ) {
    $HoL{$group} = [ get_family($group) ];
}

# likewise, but using temps
for $group ( "simpsons", "jetsons", "flintstones" ) {
    @members = get_family($group);
    $HoL{$group} = [ @members ];
}

# append new members to an existing family
push @{ $HoL{"flintstones"} }, "wilma", "betty";
</PRE>
<H2>Access and Printing of a HASH OF LISTS:</H2>
<PRE>
# one element
$HoL{flintstones}[0] = "Fred";

# another element
$HoL{simpsons}[1] =~ s/(\w)/\u$1/;

# print the whole thing 
foreach $family ( keys %HoL ) {
    print "$family: @{ $HoL{$family} }\n"
}

# print the whole thing with indices
foreach $family ( keys %HoL ) {
    print "family: ";
    foreach $i ( 0 .. $#{ $HoL{$family} } ) {
	print " $i = $HoL{$family}[$i]";
    }
    print "\n";
}

# print the whole thing sorted by number of members
foreach $family ( sort { @{$HoL{$b}} <=> @{$HoL{$b}} } keys %HoL ) {
    print "$family: @{ $HoL{$family} }\n"
}
# print the whole thing sorted by number of members and name
foreach $family ( sort { @{$HoL{$b}} <=> @{$HoL{$a}} } keys %HoL ) {
    print "$family: ", join(", ", sort @{ $HoL{$family}), "\n";
}

</PRE>

